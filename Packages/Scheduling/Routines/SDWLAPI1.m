SDWLAPI1 ;RGI/CBR - WAIT LIST API;07/17/2012
 ;;5.3;scheduling;**260003**;08/13/93
LOCK(RETURN,IEN) ;LOCK ^SDWL GLOBAL
 S RETURN=0
 I '+$G(IEN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","IEN")
 S RETURN=$$LOCK^SDWLDAL(IEN)
 Q:RETURN 1
 D ERRX^SDAPIE(.RETURN,"FILELOCK")
 Q 0
 ;
UNLOCK(IEN) ;
 Q:'+$G(IEN) 0
 Q $$UNLOCK^SDWLDAL(IEN)
 ;
HASENTRY(RETURN,DFN) ;PATIENT HAS EWL ENTRIES?
 S RETURN=0
 I '+$G(DFN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","DFN")
 S RETURN=$$HASENTRY^SDWLDAL(DFN)
 Q 1
 ;
LIST(RETURN,DFN,STATUS,BEGIN,END) ;LIST PATIENT EWL ENTRIES
 N PATOK
 S RETURN=0
 I '+$G(DFN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","DFN")
 I '$$HASENTRY(.PATOK,DFN)!'PATOK D  Q 0
 . D ERRX^SDAPIE(.RETURN,"PATNFND")
 S STATUS=$E($G(STATUS))
 S BEGIN=$G(BEGIN)
 S END=$G(END)
 D LIST^SDWLDAL(.RETURN,DFN,STATUS,BEGIN,END)
 Q 1
 ;
DETAIL(RETURN,IEN) ;
 I '+$G(IEN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","IEN")
 K RETURN
 D DETAIL^SDWLDAL(.RETURN,IEN)
 Q 1
 ;
TRNDET(RETURN,SDWLIEN) ; Get transfer details for Electronic Wait List internal entry number
 ; Input:   SDWLIEN:   EWL IEN
 ; Output:  RETURN("ACTIVE"): 0: no active transfer, 1: active transfer
 ;          RETURN("INSTITUTION"): Institution name
 ;          RETURN("STATION"):  Station Number
 N HASENTRY
 S RETURN("ACTIVE")=0
 I '+$G(SDWLIEN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","SDWLIEN")
 I '$$TRFRQACT^SDWLDAL(SDWLIEN) Q 1
 S RETURN("ACTIVE")=1
 Q $$TRFRQDET^SDWLDAL(.RETURN,SDWLIEN)
 Q 1
 ;
DISP(RETURN,SDWLDFN,SDWLIEN,SDWLDISP,SDWLAPPT) ;UPDATE DISPOSITION
 N DA,DIE,SDWLDUZ
 S RETURN=0
 I '+$G(SDWLIEN) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","SDWLIEN")
 I ($G(SDWLDISP)="")!("^D^NC^SA^CC^NN^ER^TR^CL^"'["^"_SDWLDISP) D  Q 0
 . D ERRX^SDAPIE(.RETURN,"INVPARAM","SDWLDISP")
 S RETURN=$$DISP^SDWLDAL(SDWLDFN,SDWLIEN,SDWLDISP,.SDWLAPPT)
 ;; OG ; SD*5.3*446 Inter-facility transfer.
 I SDWLDISP'="SA" D DIS^SDWLE6(SDWLIEN)
 Q RETURN
 ;
