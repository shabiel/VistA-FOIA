SDWLDISP ;;IOFO BAY PINES/TEH - WAIT LIST - DISPOSITION WAIT LIST ENTRY;08/02/12
 ;;5.3;scheduling;**263,273,427,454,446,260003**;AUG 13 1993
 ;
 ;
 ;******************************************************************
 ;                             CHANGE LOG
 ;                                               
 ;   DATE                        PATCH                   DESCRIPTION
 ;   ----                        -----                   -----------
 ;  11/19/2002                 SD*5.3*273              EN1+4 CHECK FOR "^"
 ;  11/19/2002                 SD*5.3*273              REMOVED DIC("S") SCREEN FROM PAT
 ;  08/07/2008                 SD*5.3*446              check out EWL if DFN defined
 ;  04/12/2006                 SD*5.3*446              Inter-facility transfer/New Disposition type: CL
 ;
 ;
 ;
EN ;
 N HASENTRY,SDWLIEN,SDWLDATA,SDWLERR,SDWLDISP,DIR,SDWLOP
 S SDWLOP=""
 I ($G(DFN)'>0)!('+$G(SDWLLIST)) D
 . S DFN=$$SELPAT ;SELECT PATIENT FROM EWL FILE
 . S SDWLOP=" - Disposition Patient"
 I DFN'>0 Q
 I '$$HASENTRY^SDWLAPI1(.HASENTRY,DFN)!'HASENTRY D NOENTRY Q
 N VADM
 D HD,1^VADPT,DEM^VADPT W !,VADM(1),?40,VA("PID")
 S SDWLIEN=$$SELENTRY(DFN,SDWLOP) ;SELECT EWL ENTRY TO DISPOSITION
 Q:'SDWLIEN
 I '$$LOCK^SDWLAPI1(.SDWLERR,SDWLIEN) W !,"Another User is Editing this Entry. Try Later." Q
 S %=$$DETAIL^SDWLAPI1(.SDWLDATA,SDWLIEN)
 S SDWLDISP=$$SELDISP($P(SDWLDATA("DISPTYPE"),U,2),$P(SDWLDATA("WLTYPE"),U))
 G ENQ:SDWLDISP=""
 D UPDATE(DFN,SDWLIEN,SDWLDISP,.SDWLDATA)
 W !,"*** Patient has been removed from Wait List. ***"
 K DUOUT S DIR(0)="E" D ^DIR I '$D(DUOUT) G EN
ENQ ;
 S %=$$UNLOCK^SDWLAPI1(SDWLIEN)
 Q
 ;
SELPAT() ;PATIENT LOOK-UP
 N SDWLDFN,REC
 S SDWLDFN=$$SELPAT^SDWLUI()
 Q:SDWLDFN'>0 0
 N REC
 Q:'$$PATDET^SDWLEXT(.REC,SDWLDFN) 0
 ;S SDWLNAM=$P(REC("NAME"),U,2)
 I $P(REC("DOD"),U)'="" W !!,*7,"PATIENT'S DATE OF DEATH HAS BEEN RECORDED"  ;SD*5.3*454 allow user to disposition deceased patient
 D 1^VADPT
 Q SDWLDFN
 ;
SELENTRY(SDWLDFN,SDWLOP) ;SELECT WAIT LIST ENTRY TO DISPOSITION
 N SDWLPS,SDWLY,SDWLERR,SDWLST,DIR
 D EN^SDWLD(SDWLDFN,VA("PID"),VADM(1),,1,"Wait List Disposition",SDWLOP,.SDWLST) ;DISPLAY PATIENT ENTRIES
 S SDWLPS=$S(SDWLST>1:1,SDWLST=1:2,1:0)
SELEN ;
 I SDWLPS=0 W !!,"Patient has no Wait List Entries to Disposition." S DIR(0)="E" D ^DIR Q 0
 I SDWLPS=1 S DIR(0)="FOA^^" S DIR("A")="Select Wait List (1-"_SDWLST_") or '^' to Quit? "
 I SDWLPS=2 S DIR(0)="FOA^^" S DIR("A")="Disposition This 'ENTRY' or '^' to Quit? Yes // "
 W !
 D ^DIR
 Q:X["^" 0
 S SDWLY=Y
 W !
 S:SDWLPS=1 SDWLERR=$S(X?1N.N:0,X?1"N".E:1,X?1"n".E:1,X="":1,$D(DUOUT):1,X["^":1,1:2) ;X?1"Y".E:0,X?1"y".E:0,
 I $D(SDWLERR),SDWLERR=2 W *7," Invalid Entry" G SELEN
 I SDWLPS=2 D
 . S SDWLERR=$S(X="":0,X?1"Y".E:0,X?1"y":0,X?1"N".E:1,X?1"n".E:1,X["^":1,1:2)
 . S:'SDWLY SDWLY=1
 I SDWLERR=2 W *7," Invalid Entry" G SELEN
 Q:SDWLERR!$D(DUOUT) 0
 G SELEN:'(SDWLY?1N.N)
 I '$D(SDWLST(+SDWLY)) W " Invalid Entry " G SELEN
 Q SDWLST(+SDWLY,"IEN")
 ;
SELDISP(SDWLDISN,SDWLTY) ;ENTER DISPOSITION
 N DIR,SDWLDISP
SELD ;
 S DIR("B")=SDWLDISN
 S DIR(0)="SO^D:DEATH;NC:REMOVED/NON-VA CARE;SA:REMOVED/SCHEDULED-ASSIGNED;CC:REMOVED/VA CONTRACT CARE;NN:REMOVED/NO LONGER NECESSARY;ER:ENTERED IN ERROR;CL:CLINIC CHANGE^"
 S DIR("L",1)="Disposition Reason:"
 S DIR("L",2)=""
 S DIR("L",3)="D DEATH"
 S DIR("L",4)="NC REMOVED/NON-VA CARE"
 S DIR("L",5)="SA REMOVED/SCHEDULED-ASSIGNED"
 S DIR("L",6)="CC REMOVED/VA CONTRACT CARE"
 S DIR("L",7)="NN REMOVED/NO LONGER NECESSARY"
 S DIR("L")="ER ENTERED IN ERROR"
 S:SDWLTY=4 DIR("L",8)="CL CLINIC CHANGE"
 D ^DIR
 I X="" Q ""
 I X="^" Q ""
 S SDWLDISP=$TR(X,"acdelnrst","ACDELNRST")
 I "^D^NC^SA^CC^NN^ER^TR^"_$S(SDWLTY=4:"CL^",1:"")'[("^"_SDWLDISP_"^") W *7,"Invalid Entry" G SELD
 Q SDWLDISP
 ;
SELAPPT(SDWLIEN,SDWLDATA,SDWLAPPT) ;SELECT APPOINTMENT TO CLOSE WITH
 N SDWLTY,SDCL,SDSP,SDORG,SDDS,SDAP,SDA,DIR,X
 S SDWLTY=$P(SDWLDATA("WLTYPE"),U)
 S (SDCL,SDSP)=""
 S:SDWLTY=4 SDCL=$P(SDWLDATA("WAITFORP"),U)
 S:SDWLTY=3 SDSP=$P(SDWLDATA("WAITFORP"),U)
 S SDORG=$P(SDWLDATA("ORIGDT"),U)
 S SDDS=$$CHKENC^SDWLQSC1(SDWLIEN,SDORG,SDCL,SDSP,1)
 S SDAP=$O(^TMP($J,"APPT",""))
 Q:SDAP="" 1
 D APPTD^SDWLEVAL
 I $O(^TMP($J,"APPT",""))'=$O(^TMP($J,"APPT",""),-1) D
 . W !
 . S SDA=$O(^TMP($J,"APPT",""),-1)
 . I SDA=1 S DIR("B")=1
 . S DIR(0)="N^1:"_SDA S DIR("A")="Select appt for Removal Reason or '^' to Quit>",DIR("?")="Select Appointment to close with the open EWL."
 . D ^DIR
 . S SDAP=X
 Q:SDAP="^" 0
 Q:'SDAP 1
 D DATP^SDWLEVAL(SDAP,.SDWLAPPT)
 Q 1
 ;
UPDATE(SDWLDFN,SDWLIEN,SDWLDISP,SDWLDATA) ;UPDATE EWL ENTRY
 N SDWLERR,SDWLAPPT,SDWLTY
 S SDWLTY=$P(SDWLDATA("WLTYPE"),U)
 I SDWLDISP="SA","3,4"[SDWLTY S SDWLERR='$$SELAPPT(SDWLIEN,.SDWLDATA,.SDWLAPPT) Q:SDWLERR  ; QUIT OR NOT?
 I SDWLDISP="CL" S SDWLERR=$$EN^SDWLE7 Q:SDWLERR  ; OG ; 446
 S %=$$DISP^SDWLAPI1(.SDWLERR,SDWLDFN,SDWLIEN,SDWLDISP,.SDWLAPPT)
 Q
 ;
NOENTRY ;SHOW PATIENT HAS NO ENTRIES MESSAGE
 N VADM
 D HD,1^VADPT,DEM^VADPT
 W !,VADM(1),?40,VA("PID"),*7,!,"This Patient has NO entries on the Electronic Wait List."
 S DIR(0)="E" D ^DIR
 Q
 ;
HD ;HEADER
 ;
 W:$D(IOF) @IOF W !!,?80-$L("Wait List - Disposition Patient")\2,"Wait List - Disposition Patient",!!
 Q
 ;
