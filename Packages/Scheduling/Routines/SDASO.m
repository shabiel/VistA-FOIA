SDASO ;MAN/GRR - APPEND TESTS TO PENDING APPOINTMENT ; 10/23/2012
 ;;5.3;Scheduling;**260003**;Aug 13, 1993
 S:'$D(DTIME) DTIME=300 D:'$D(DT) DT^SDUTL S HDT=DT,APL=""
RD ;
 N DFN,APTS,SDT,L,CNT
 S DFN=$$SELPAT^SDMUTL("Patient") G END:DFN<0
 S %=$$LSTPAPTS^SDMAPI1(.APTS,+DFN,DT,,"ALL")
 S SDT=0,L=0,CNT=0
 S NAME=$P(DFN,"^",2) W ! I $O(APTS(""))'>0 G NO
 F SDT=0:0 S SDT=$O(APTS(SDT)) Q:'SDT  D
 . S STAT=APTS(SDT,"STATI")
 . I $S(STAT']"":1,STAT["I":1,1:0) D
 . . N TST,SEP,COMM S L=L+1,SEP="",TST="",COMM=""
 . . S:$G(APTS(SDT,"LAB"))>0 TST="LAB",SEP=","
 . . S:$G(APTS(SDT,"XRAY"))>0 TST=TST_SEP_"XRAY",SEP=","
 . . S:$G(APTS(SDT,"EKG"))>0 TST=TST_SEP_"EKG",SEP=","
 . . S:$L(TST)>0 COMM="("_TST_" TEST SCHEDULED)"
 . . S Z(L)=APTS(SDT,"SD")_U_APTS(SDT,"SC")_U_APTS(SDT,"LEN")_U_COMM
 . . S Z(L)=Z(L)_U_APTS(SDT,"CLINIC")_U_APTS(SDT,"OE")
 G:L'>0 NO
 F ZZ=1:1:L  D
 . W !!,ZZ,") "
 . S Y=$P(Z(ZZ),"^",1) D AT^SDUTL S Y=$P(Y,"@",1)_" "_$P(Y,"@",2)
 . W " ",$J(Y,8)," (",$P(Z(ZZ),"^",3)," MINUTES)  ",$P(Z(ZZ),"^",5)," ",$P(Z(ZZ),"^",4)
WH R !!,"SCHEDULE TESTS FOR WHICH NUMBERED APPOINTMENT: ",APP:DTIME G:APP=""!(APP="^") RD I APP?."?" D HLP G WH
 I APP'?1N.N W !,"INVALID ENTRY, MUST BE NUMERIC" G WH
 I $L(APP)>5 W !,"ENTER A NUMBER BETWEEN 1 AND ",ZZ G WH
 I APP<1!(APP>ZZ) W !,"ENTER A NUMBER BETWEEN 1 AND ",ZZ G WH
 S %=$$ISOECO^SDMAPI4(.ERR,DFN,+Z(APP),"add")
 I ERR W !,*7 D MSG^DIALOG("WE",,,,"ERR") G WH
 S SD=$P(Z(APP),"^",1) S CNT=CNT+1,Y=SD D DTS^SDUTL
 S SODT=Y,SDWR=0 N LAB,XRAY,EKG,ERR
 D ORDY^SDM3(.LAB,.XRAY,.EKG) G:'$G(LAB)&'$G(XRAY)&'$G(EKG) RD
 S %=$$ADDTSTS^SDMAPI4(.ERR,+DFN,SD,.LAB,.XRAY,.EKG)
 I ERR=0 W !,*7 D MSG^DIALOG("WE",,,,"ERR")
 G RD
NOPE W:'CNT !,*7,"NOTHING SCHEDULED" G RD
NO W !,"NO PENDING APPOINTMENTS",*7,*7,*7
 G RD
END K CNT,NDT,L,J,HDT,SC,SD,APL,COMMENT,Z,ZZ,APP,ZL,SDJ,X,%DT,DIC,DFN,NAME,Y,POP,SDAPAV,SDTY Q
HLP W !,"Enter the number that corresponds to the appointment." Q
 ;
