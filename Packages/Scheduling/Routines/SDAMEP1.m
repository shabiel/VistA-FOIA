SDAMEP1 ;ALB/CAW - Expanded Display (Appt. Data) ; 08/31/2012
 ;;5.3;Scheduling;**20,241,534,260003**;Aug 13, 1993;Build 8
 ;
APDATA ; Appointment Data
 ;
 D SET($$SETSTR^VALM1("*** Appointment Demographics ***","",24,32))
 D CNTRL^VALM10(SDLN,24,32,IOINHI,IOINORM)
 D SET("")
 ;
 S X=""
 S X=$$SETSTR^VALM1("           Name:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($G(CAPT("PATIENT")),X,SDFSTCOL,24)
 S X=$$SETSTR^VALM1("         Clinic:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1($G(APT("CLINIC")),X,SDSECCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1("             ID:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1(VA("PID"),X,SDFSTCOL,24)
 S X=$$SETSTR^VALM1("      Date/Time:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1($$FTIME^VALM1(SDT),X,SDSECCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1("         Status:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($P($G(CAPT("STATUS")),";",3),X,SDFSTCOL,50)
 D SET(X)
 ;
 S SDPV=APT("PURPOSE","I"),SDPOV=$S(SDPV=1:"C&P",SDPV=2:"10-10",SDPV=3:"SCHEDULED",SDPV=4:"UNSCHEDULED",1:"UNKNOWN")
 S X="",X=$$SETSTR^VALM1("Purpose of Vst.:",X,1,16)
 S X=$$SETSTR^VALM1(SDPOV,X,SDFSTCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1(" Length of Appt:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($G(CAPT("LENGTH")),X,SDFSTCOL,4)
 S X=$$SETSTR^VALM1("      Appt Type:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1(APT("TYPE"),X,SDSECCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1("            Lab:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($P(APT("LABDT"),"@",2),X,SDFSTCOL,5)
 S X=$$SETSTR^VALM1("   Elig of Appt:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1($G(CAPT("EVISIT")),X,SDSECCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1("          X-ray:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($P(APT("XRAYDT"),"@",2),X,SDFSTCOL,5)
 S X=$$SETSTR^VALM1("       Overbook:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1($G(CAPT("OVERBOOK")),X,SDSECCOL,24)
 D SET(X)
 ;
 S X=""
 S X=$$SETSTR^VALM1("            EKG:",X,1,SDWIDTH)
 S X=$$SETSTR^VALM1($P(APT("EKGDT"),"@",2),X,SDFSTCOL,5)
 S X=$$SETSTR^VALM1("Collateral Appt:",X,40,SDWIDTH)
 S X=$$SETSTR^VALM1($G(APT("CVISIT")),X,SDSECCOL,17)
 D SET(X)
 ;
 S X=""
 S INFO=$G(CAPT("OTHER"))
 N SDINFL S SDINFL=$L($G(INFO)) ; lenght of INFO STRING
 I SDINFL<64 D
 .S X=$$SETSTR^VALM1("          Other:",X,1,SDWIDTH)
 .S X=$$SETSTR^VALM1($G(INFO),X,SDFSTCOL,63)
 I SDINFL>63&(SDINFL<143) D
 .S X=$$SETSTR^VALM1("          Other:",X,1,SDWIDTH)
 .S X=$$SETSTR^VALM1($E($G(INFO),1,64),X,17,80)
 .D SET(X)
 .S X=$$SETSTR^VALM1("",X,1,0)
 .S X=$$SETSTR^VALM1($E($G(INFO),65,150),X,1,80)
 I SDINFL>142 D
 .S X=$$SETSTR^VALM1("   Other:",X,1,10)
 .S X=$$SETSTR^VALM1($E($G(INFO),1,70),X,11,80)
 .D SET(X)
 .S X=$$SETSTR^VALM1("",X,1,0)
 .S X=$$SETSTR^VALM1($E($G(INFO),71,150),X,1,80)
 D SET(X)
 ;
 K ENRS,ENI,ENR
 S %=$$GETPENRL^SDMAPI3(.ENRS,DFN,APT("CLINIC","I"),1)
 S ENI=$O(ENRS(APT("CLINIC","I"),"EN",9999999),-1)
 M:ENI ENR=ENRS(APT("CLINIC","I"),"EN",ENI)
 D ENROLL
 D SET($S($D(SDFLG):X,1:" "))
 S X="",X=$$SETSTR^VALM1($S('$D(ENR):"",ENR("ENROLLMENT")="":"",ENR("DISCHARGE")="":"Enrollment Date/Time:",1:""),X,4,21)
 I $D(ENR),ENR("DISCHARGE")="" S X=$$SETSTR^VALM1($$FTIME^VALM1(ENR("ENROLLMENT")),X,26,18)
 D SET(X)
 Q
 ;
ENROLL ;
 S SDFLG=1
 S X="",X=$$SETSTR^VALM1("Enrolled in this clinic:",X,1,25)
 S X=$$SETSTR^VALM1($S('$D(ENR):"NO",ENR("ENROLLMENT")="":"NO",ENR("DISCHARGE")'="":"NO",1:"YES"),X,26,3)
 S X=$$SETSTR^VALM1($S('$D(ENR):"",ENR("ENROLLMENT")="":"",ENR("DISCHARGE")="":" OPT or AC:",ENR("DISCHARGE")'="":"Disch fm Clinic:",1:""),X,44,17)
 I $D(ENR),ENR("DISCHARGE")="" S X=$$SETSTR^VALM1($S(ENR("OA")="A":"AC",1:"OPT"),X,SDSECCOL,3)
 I $D(ENR),ENR("DISCHARGE")'="" S X=$$SETSTR^VALM1($$FTIME^VALM1(ENR("DISCHARGE")),X,62,17)
 Q
SET(X) ; Set in ^TMP global for display
 ;
 S SDLN=SDLN+1,^TMP("SDAMEP",$J,SDLN,0)=X
 Q
 ;
INIT ; -- set up vars
 N DR,DIQ,DIC,DA
 D PID^VADPT6
 S SDFSTCOL=18,SDWIDTH=16,SDSECCOL=57
 K APT,CAPT,SDOE
 S %=$$GETPAPT^SDMAPI4(.APT,DFN,SDT)
 S %=$$GETCAPT^SDMAPI4(.CAPT,DFN,SDT)
 S %=$$GETOE^SDMAPI4(.SDOE,APT("OE","I"))
 S SDOE=APT("OE","I")
 Q
