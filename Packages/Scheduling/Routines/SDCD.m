SDCD ;BSN/GRR - DISCHARGE PATIENT FROM CLINIC ;09/24/2012
 ;;5.3;Scheduling;**41,148,260003**;AUG 13, 1993
15 D:'$D(DT) DT^SDUTL
 N ENRLS,SENS,SDAMERR
 S SDAMERR=0
 I '$G(SDFN) S Y=$$SELPAT^SDMUTL() S:Y'=-1 DFN=+Y G:Y=-1 QUIT
 S:$G(SDFN) DFN=+SDFN
 I '$G(SDCLN) S %=$$GETPENRL^SDMAPI3(.ENRLS,DFN,,1) G:'$D(ENRLS) NOPE
 D:'$G(SDCLN) SELCLN(.ENRLS,.SENS,"clinic")
 I $G(SDCLN) D  G QUIT:SDAMERR>0
 . S %=$$GETPENRL^SDMAPI3(.SENS,DFN,SDCLN)
 . I '$D(SENS(SDCLN,"EN")) D
 . . W !!,*7,">>> Patient not enrolled in '",SENS(SDCLN,"NAME"),"' clinic." S SDAMERR=1
 . I $G(SENS(SDCLN,"STATUS"))]"" D
 . . W *7,*7,!,"PATIENT ALREADY DISCHARGED FROM '",SENS(SDCLN,"NAME"),"' CLINIC",*7,*7
 . . S SDAMERR=1 Q
 N IND
 F IND=0:0 S IND=$O(SENS(IND)) Q:IND=""  D
 . W !!,"***Discharging patient from ",SENS(IND,"NAME")," Clinic***",!
 . N ENS
 . M ENS(IND)=SENS(IND)
 . D DISCH(.ENS,DFN,IND)
 G 15:'$G(SDFN)
QUIT ;
 K CLINIC,DFN,SC,SDF,SDST,VAUTC,VAUTD,VAUTNI,VAUSTR,VAUTVB,DIC
 Q
DISCH(ENS,DFN,SC) ;
 N RETURN
 D COL(.ENS,SC)
 S %=$$DISCH^SDMAPI3(.RETURN,.ENS,DFN)
 I RETURN=0 W $P(RETURN(0),U,2) Q
 W !,*7,"Patient Discharged from clinic !!",!
 Q
 ;
COL(ENS,SC) ;
 N IND
 F IND=0:0 S IND=$O(ENS(SC,"EN",IND)) Q:'IND  D
 . I ENS(SC,"EN",IND,"DISCHARGE")']"" D
 . . K X,Y
 . . S DIR("A")="DATE OF DISCHARGE: "
 . . S DIR(0)="DA^"_($E(DT,1,3)-80)_":"_DT_":EX" D ^DIR
 . . S:Y>0 ENS(SC,"EN",IND,"DISCHARGE")=Y
 . . Q:Y="^"
 . . K X,Y
 . . S DIR("A")="REASON FOR DISCHARGE: "
 . . S:$D(ENS(SC,"EN",IND,"REASON")) DIR("B")=ENS(SC,"EN",IND,"REASON")
 . . S DIR(0)="FA^2:80:EX" D ^DIR
 . . S:Y'="^" ENS(SC,"EN",IND,"REASON")=Y
 K DIR
 Q
 ;
NOPE W !,*7,"PATIENT NOT ENROLLED IN ANY CLINICS!!" G QUIT:$G(SDFN),15
SELCLN(ENS,SENS,NAME) ; Select clinics
 N LST,SLST,CNT
 S CNT=0
 F IND=0:0 S IND=$O(ENS(IND)) Q:IND=""  D
 . S CNT=CNT+1
 . S LST(CNT,"ID")=IND
 . S LST(CNT,"NAME")=ENS(IND,"NAME")
 S LST(0)=CNT
 D SELSLST^SDMUTL(.LST,.SLST,"clinic")
 K SENS
 F IND=0:0 S IND=$O(SLST(IND)) Q:IND=""  D
 . M SENS(SLST(IND,"ID"))=ENS(SLST(IND,"ID"))
 Q
