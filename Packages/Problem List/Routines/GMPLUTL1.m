GMPLUTL1 ; SLC/MKB/KER -- PL Utilities (cont)               ; 09/14/12
 ;;2.0;Problem List;**3,8,7,9,26,35,39,260002**;Aug 25, 1994
 ;
 ; External References
 ;   DBIA   446  ^AUTNPOV(
 ;   DBIA 10082  ^ICD9(
 ;   DBIA  1571  ^LEX(757.01
 ;   DBIA 10040  ^SC(
 ;   DBIA 10060  ^VA(200
 ;   DBIA 10003  ^%DT
 ;   DBIA 10104  $$UP^XLFSTR
 ;                   
 ; All entry points in this routine expect the 
 ; PL("data item") array from routine ^GMPLUTL.
 ;                   
 ;   Entry     Expected Variable
 ;   Point     From VADPT^GMPLX1
 ;    AO           GMPAGTOR
 ;    IR           GMPION
 ;    EC           GMPGULF
 ;    HNC          GMPHNC
 ;    MST          GMPMST
 ;    CV           GMPCV
 ;    SHD          GMPSHD
 ;                   
 Q
DIAGNOSI ; ICD Diagnosis Pointer
 S:'$L($G(PL("DIAGNOSIS"))) PL("DIAGNOSIS")=$$NOS^GMPLEXT()
 Q:$$ICDCODE^GMPLEXT(+PL("DIAGNOSIS"))=""
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.461)
 Q
 ;
LEXICON ; Clinical Lexicon Pointer
 S:'$L($G(PL("LEXICON"))) PL("LEXICON")=1
 Q:$$CONTEXT^GMPLEXT(+PL("LEXICON"))'=""
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.462)
 Q
DUPLICAT ; Problem Already on the List 
 N DUPL,GMPARAM
 D GET^GMPLSITE(.GMPARAM)
 Q:GMPARAM("SDP")'=1
 S:'$L($G(PL("DIAGNOSIS"))) PL("DIAGNOSIS")=$$NOS^GMPLEXT()
 S %=$$DUPL^GMPLAPI2(.DUPL,GMPDFN,+PL("DIAGNOSIS"),PL("NARRATIVE"),1)
 I DUPL S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.463)
 Q
 ;
LOCATION ; Hospital Location (Clinic) Pointer
 S:'$D(PL("LOCATION")) PL("LOCATION")="" Q:'$L(PL("LOCATION"))
 I $$CLINNAME^GMPLEXT(+PL("LOCATION")) S:$$LOCTYPE^GMPLEXT(+PL("LOCATION"))'="C" PL("LOCATION")="" Q
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.464)
 Q
 ;
PROVIDER ; Responsible Provider
 S:'$D(PL("PROVIDER")) PL("PROVIDER")=""
 Q:'$L(PL("PROVIDER"))  Q:$$ACTIVE^XUSER(+PL("PROVIDER"))'=""
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.454)
 Q
 ;
STATUS ; Problem Status
 S:$G(PL("STATUS"))="" PL("STATUS")="A"
 I "^A^I^a^i^"[(U_PL("STATUS")_U) S PL("STATUS")=$$UP^XLFSTR(PL("STATUS")) Q
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.467)
 Q
 ;
ONSET ; Date of Onset
 N %DT,Y,X
 S:'$D(PL("ONSET")) PL("ONSET")="" Q:'$L(PL("ONSET"))
 S %DT="P",%DT(0)="-NOW",X=PL("ONSET") D ^%DT
 I Y>0 S PL("ONSET")=Y Q
 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.468)
 Q
 ;
RESOLVED ; Date Resolved (Requires STATUS, ONSET)
 N %DT,Y,X
 S:'$D(PL("RESOLVED")) PL("RESOLVED")="" Q:'$L(PL("RESOLVED"))
 S %DT="P",%DT(0)="-NOW",X=PL("RESOLVED") D ^%DT
 I Y'>0 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.469) Q
 I PL("STATUS")="A" S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.470) Q
 I Y<PL("ONSET") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.459) Q
 S PL("RESOLVED")=Y
 Q
 ;
RECORDED ; Date Recorded (Requires ONSET)
 N %DT,Y,X
 S:'$D(PL("RECORDED")) PL("RECORDED")="" Q:'$L(PL("RECORDED"))
 S %DT="P",%DT(0)="-NOW",X=PL("RECORDED") D ^%DT
 I Y'>0 S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.472) Q
 I PL("RECORDED")<PL("ONSET") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.460) Q
 S PL("RECORDED")=Y
 Q
 ;
SC ; SC condition flag
 S:'$D(PL("SC")) PL("SC")=""
 I "^^1^0^"'[(U_PL("SC")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.471) Q
 I 'GMPSC,+PL("SC") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.471)
 Q
 ;
AO ; AO exposure flag (Requires GMPAGTOR)
 S:'$D(PL("AO")) PL("AO")=""
 I "^^1^0^"'[(U_PL("AO")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.473) Q
 I 'GMPAGTOR,+PL("AO") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.473)
 Q
 ;
IR ; IR exposure flag (Requires GMPION)
 S:'$D(PL("IR")) PL("IR")=""
 I "^^1^0^"'[(U_PL("IR")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.474) Q
 I 'GMPION,+PL("IR") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.474)
 Q
 ;
EC ; EC exposure flag (Requires GMPGULF)
 S:'$D(PL("EC")) PL("EC")=""
 I "^^1^0^"'[(U_PL("EC")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.475) Q
 I 'GMPGULF,+PL("EC") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.475)
 Q
HNC ; HNC/NTR exposure flag (Requires GMPHNC)
 S:'$D(PL("HNC")) PL("HNC")=""
 I "^^1^0^"'[(U_PL("HNC")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.476) Q
 I 'GMPHNC,+PL("HNC") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.476)
 Q
MST ; MST exposure flag (Requires GMPMST)
 S:'$D(PL("MST")) PL("MST")=""
 I "^^1^0^"'[(U_PL("MST")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.477) Q
 I 'GMPMST,+PL("MST") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.477)
 Q
CV ; CV exposure flag (Requires GMPCV)
 S:'$D(PL("CV")) PL("CV")=""
 I "^^1^0^"'[(U_PL("CV")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.478) Q
 I 'GMPCV,+PL("CV") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.478)
 Q
SHD ; SHD exposure flag (Requires GMPSHD)
 S:'$D(PL("SHD")) PL("SHD")=""
 I "^^1^0^"'[(U_PL("SHD")_U) S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.479) Q
 I 'GMPSHD,+PL("SHD") S GMPQUIT=1,PLY(0)=$$EZBLD^DIALOG(1250000.479)
 Q
