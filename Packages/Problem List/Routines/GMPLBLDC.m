GMPLBLDC ; SLC/MKB -- Build Problem Selection Categories ;09/14/12
 ;;2.0;Problem List;**3,7,28,260002**;Aug 25, 1994
 ;
 ; This routine invokes IA #3991
 ;
EN ; -- main entry point for GMPL SELECTION GROUP BUILD
 D EN^VALM("GMPL SELECTION GROUP BUILD")
 Q
 ;
HDR ; -- header code
 N NAME,NUM,DATE S NUM=+^TMP("GMPLST",$J,0)_$S(+^TMP("GMPLST",$J,0)'=1:$$EZBLD^DIALOG(1250000.055),1:$$EZBLD^DIALOG(1250000.057))
 S DATE=$$EZBLD^DIALOG(1250000.038)_^TMP("GMPLIST",$J,"CAT","MODIFIED")
 S VALMHDR(1)=DATE_$J(NUM,79-$L(DATE))
 S NAME=^TMP("GMPLIST",$J,"CAT","NAME"),VALMHDR(2)=$J(NAME,$L(NAME)\2+41)
 Q
 ;
INIT ; -- init variables and list array
 N RETURN,MSG
 S GMPLGRP=$$GROUP^GMPLBLD2("L") I GMPLGRP="^" S VALMQUIT=1 Q
 I '$$LOCKCAT^GMPLAPI1(.RETURN,GMPLGRP) D  G INIT
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.073,,,"MSG")
 . D EN^DDIOL(.MSG)
 S GMPLMODE="E",VALMSG=$$MSG^GMPLX
 K MSG
 D BLD^DIALOG(1250000.126,,,"MSG")
 D EN^DDIOL(.MSG)
 D GETLIST,BUILD("^TMP(""GMPLIST"",$J)",GMPLMODE)
 Q
 ;
GETLIST ; Build ^TMP("GMPLIST",$J,#) of problems
 K ^TMP("GMPLIST",$J)
 N RETURN
 S %=$$GETCAT^GMPLAPI1(.RETURN,GMPLGRP)
 M ^TMP("GMPLIST",$J)=RETURN
 Q
 ;
BUILD(LIST,MODE) ; Build ^TMP("GMPLST",$J,) of current items in LIST for display
 N SEQ,LCNT,NUM,PROB,TEXT,IFN,ITEM,CODE D CLEAN^VALM10
 I $P($G(^TMP("GMPLIST",$J,0)),U,1)'>0 S ^TMP("GMPLST",$J,1,0)="   ",^TMP("GMPLST",$J,2,0)=$$EZBLD^DIALOG(1250000.127),^TMP("GMPLST",$J,0)="0^2",VALMCNT=2 Q
 S (LCNT,NUM,SEQ)=0
 F  S SEQ=$O(^TMP("GMPLIST",$J,"SEQ",SEQ)) Q:SEQ'>0  D
 . S IFN=^TMP("GMPLIST",$J,"SEQ",SEQ),LCNT=LCNT+1,NUM=NUM+1,CODE=""
 . S PROB=$P(^TMP("GMPLIST",$J,IFN),U,2),TEXT=$P(^TMP("GMPLIST",$J,IFN),U,3)
 . S:$D(^TMP("GMPLIST",$J,IFN,"CODE"))>0 CODE=^TMP("GMPLIST",$J,IFN,"CODE")
 . S ^TMP("GMPLST",$J,LCNT,0)=$S(MODE="I":$J("<"_SEQ_">",8),1:"        ")_$J(NUM,4)_" "_TEXT
 . I $L(CODE) D
 .. S ^TMP("GMPLST",$J,LCNT,0)=^TMP("GMPLST",$J,LCNT,0)_" ("_$P(CODE,U,1)_")"
 .. S FLAG=$P(CODE,U,2)
 .. Q:$G(FLAG)="0"  ; code is active
 .. S ^TMP("GMPLST",$J,LCNT,0)=^TMP("GMPLST",$J,LCNT,0)_$$EZBLD^DIALOG(1250000.128)
 . D CNTRL^VALM10(LCNT,9,5,IOINHI,IOINORM)
 . S ^TMP("GMPLST",$J,"B",NUM)=IFN
 S ^TMP("GMPLST",$J,0)=NUM_U_LCNT,VALMCNT=LCNT
 Q
 ;
HELP ; -- help code
 N X,MSG
 D BLD^DIALOG(1250000.129,,,"MSG")
 D EN^DDIOL(.MSG)
 R X:DTIME
 S VALMSG=$$MSG^GMPLX,VALMBCK=$S(VALMCC:"",1:"R")
 Q
 ;
EXIT ; -- exit code
 I $D(GMPLSAVE),$$CKSAVE^GMPLBLD2 D
 . D SAVE^GMPLBLD2
 D UNLKCAT^GMPLAPI1(+GMPLGRP)
 K GMPLIST,GMPLST,GMPLMODE,GMPLGRP,GMPLSAVE,GMPREBLD,GMPQUIT,RT1,TMPITEM
 K VALMBCK,VALMCNT,VALMSG,VALMHDR
 K ^TMP("GMPLIST",$J),^TMP("GMPLST",$J)
 Q
 ;
ADD ; Add new problem(s)
 N X,Y,SEQ,CODE,IFN,GMPVOCAB,GMPQUIT,GMPREBLD S VALMBCK=""
 S GMPVOCAB=$$VOCAB^GMPLX1 Q:GMPVOCAB="^"
 F  D  Q:$D(GMPQUIT)  W !!
 . S (X,Y)="" D SEARCH^GMPLX(.X,.Y,$$EZBLD^DIALOG(1250000.130),"1",GMPVOCAB)
 . I +Y'>0 S GMPQUIT=1 Q
 . S X=$$TEXT^GMPLBLD1(X) I X="^" S GMPQUIT=1 Q
 . S CODE=$$CODE^GMPLBLD1($G(Y(1))) I CODE="^" S GMPQUIT=1 Q
 . S RT1="^TMP(""GMPLIST"",$J,""SEQ"",",SEQ=+$$LAST^GMPLBLD2(RT1)+1 ; dflt = next #
 . S SEQ=$$SEQ^GMPLBLD1(SEQ) I SEQ="^" S GMPQUIT=1 Q
 . S IFN=$$TMPIFN^GMPLBLD1,^TMP("GMPLIST",$J,0)=^TMP("GMPLIST",$J,0)+1
 . S ^TMP("GMPLIST",$J,IFN)=SEQ_U_+Y_U_X_U_CODE ; seq ^ # ^ text ^ code
 . S ^TMP("GMPLIST",$J,IFN,"CODE")=CODE_U_"0" ; code ^ active flag
 . S (^TMP("GMPLIST",$J,"PROB",+Y),^TMP("GMPLIST",$J,"SEQ",SEQ))=IFN,GMPREBLD=1
 I $D(GMPREBLD) S VALMBCK="R",GMPLSAVE=1 D BUILD("^TMP(""GMPLIST"",$J)",GMPLMODE),HDR
 S:'VALMCC VALMBCK="R" S VALMSG=$$MSG^GMPLX
 Q
 ;
REMOVE ; Remove problem from group
 N NUM,IFN,MSG S VALMBCK=""
 S NUM=$$SEL1^GMPLBLD1 G:NUM="^" RMQ
 S IFN=$P($G(^TMP("GMPLST",$J,"B",NUM)),U,1) G:+IFN'>0 RMQ
 I "@"[$G(^TMP("GMPLIST",$J,IFN)) D  H 2 G RMQ
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.131,,,"MSG")
 . D EN^DDIOL(.MSG)
 I '$$SURE^GMPLX D  H 1 G RMQ
 . D BLD^DIALOG(1250000.132,,,"MSG")
 . D EN^DDIOL(.MSG)
 D DELETE^GMPLBLD1(IFN) S VALMBCK="R",GMPLSAVE=1
 D BUILD("^TMP(""GMPLIST"",$J)",GMPLMODE),HDR
RMQ S:'VALMCC VALMBCK="R" S VALMSG=$$MSG^GMPLX
 Q
 ;
EDIT ; Edit problem text and code
 N NUM,SEL,IFN,PIECE,CODE,PROB,PROBLEM,GMPQUIT,GMPREBLD,MSG S VALMBCK=""
 S SEL=$$SEL^GMPLBLD1 G:SEL="^" EDQ
 F PIECE=1:1:$L(SEL,",") D  Q:$D(GMPQUIT)  W !
 . S NUM=$P(SEL,",",PIECE) Q:NUM'>0
 . S IFN=$P($G(^TMP("GMPLST",$J,"B",NUM)),U,1) Q:IFN'>0
 . I "@"[$G(^TMP("GMPLIST",$J,IFN)) D  H 2 Q
 . . D EN^DDIOL($C(7))
 . . K MSG
 . . D BLD^DIALOG(1250000.133,NUM,,"MSG")
 . . D EN^DDIOL(.MSG)
 . K MSG
 . D BLD^DIALOG(1250000.134,NUM,,"MSG")
 . D EN^DDIOL(.MSG)
 . S PROBLEM=^TMP("GMPLIST",$J,IFN)
 . D:$P(PROBLEM,U,2)>1 EN^DDIOL(" = "_$$CONTEXT^GMPLEXT(+$P(PROBLEM,U,2))) ; KER
 . S PROB=$$TEXT^GMPLBLD1($P(PROBLEM,U,3)) I PROB="^" S GMPQUIT=1 Q
 . I PROB="@" D DELETE^GMPLBLD1(IFN) S GMPREBLD=1 Q
 . S CODE=$$CODE^GMPLBLD1($P(PROBLEM,U,4)) I CODE="^" S GMPQUIT=1 Q
 . S ^TMP("GMPLIST",$J,IFN)=$P(PROBLEM,U,1,2)_U_PROB_U_CODE,GMPREBLD=1
 I $D(GMPREBLD) S VALMBCK="R",GMPLSAVE=1 D BUILD("^TMP(""GMPLIST"",$J)",GMPLMODE)
EDQ S:'VALMCC VALMBCK="R" S VALMSG=$$MSG^GMPLX
 Q
