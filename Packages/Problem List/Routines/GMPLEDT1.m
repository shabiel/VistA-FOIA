GMPLEDT1 ; SLC/MKB/KER/AJB -- Edit Problem List fields ; 04/21/2003
 ;;2.0;Problem List;**17,20,26,28,35**;Aug 25, 1994;Build 26
 ;
 ; External References
 ;   DBIA 10006  ^DIC
 ;   DBIA 10026  ^DIR
 ;   DBIA   341  DIS^SDROUT2
 ;                
ONSET ; Edit Date of Onset - field .13
 N X,Y,ENTERED,PROMPT,HELPMSG,DEFAULT,MSG
 S ENTERED=$S($G(GMPFLD(.08)):+GMPFLD(.08),1:DT),DEFAULT=$G(GMPFLD(.13))
 S PROMPT=$$EZBLD^DIALOG(1250000.209),HELPMSG=$$EZBLD^DIALOG(1250000.210)
O1 ;   Get Date of Onset
 D DATE^GMPLEDT2 Q:$D(GMPQUIT)!($G(GMPLJUMP))
 I Y>ENTERED D  G O1
 . D BLD^DIALOG(1250000.211,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 I +$P(GMPDFN,U,4),Y>$P(GMPDFN,U,4) D  G O1
 . K MSG
 . D BLD^DIALOG(1250000.212,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 S GMPFLD(.13)=Y S:Y'="" GMPFLD(.13)=GMPFLD(.13)_U_$$EXTDT^GMPLX(Y)
 Q
STATUS ; Edit Status - field .12
 ;   Then Edit Date Resolved - Field 1.07, if inactive
 N DIR,X,Y
 S DIR(0)="9000011,.12"
 S:$L($G(GMPFLD(.12))) DIR("B")=$P(GMPFLD(.12),U,2)
ST1 ;   Get Status
 D ^DIR I $D(DTOUT)!(Y="^") S GMPQUIT=1 Q
 I Y?1"^".E D JUMP^GMPLEDT3(Y) Q:$D(GMPQUIT)!($G(GMPLJUMP))  K:$G(GMPIFN) GMPLJUMP G ST1
 S:Y'="" Y=Y_U_$S(Y="A":$$EZBLD^DIALOG(1250000.182),1:$$EZBLD^DIALOG(1250000.183)) S GMPFLD(.12)=Y
 S:$E(Y)'="I" GMPFLD(1.07)="" S:$E(Y)'="A" GMPFLD(1.14)=""
 D:$E(GMPFLD(.12))="I" RESOLVED^GMPLEDT4
 D:$E(GMPFLD(.12))="A" PRIORITY^GMPLEDT4
 Q
RECORDED ; Edit Date Recorded - field 1.09
 N X,Y,PROMPT,HELPMSG,DEFAULT,ENTERED,MSG
 S ENTERED=$S($G(GMPFLD(.08)):+GMPFLD(.08),1:DT),DEFAULT=$G(GMPFLD(1.09))
 S PROMPT=$$EZBLD^DIALOG(1250000.213),HELPMSG=$$EZBLD^DIALOG(1250000.214)
RC1 ;   Get Date
 D DATE^GMPLEDT2 Q:$D(GMPQUIT)!($G(GMPLJUMP))
 I Y>ENTERED D  G RC1
 . D BLD^DIALOG(150000.215,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 S GMPFLD(1.09)=Y S:Y'="" GMPFLD(1.09)=GMPFLD(1.09)_U_$$EXTDT^GMPLX(Y)
 Q
SC ; Edit Service Connected - field 1.1
 N DFN,DIR,X,Y,MSG
 ;
 ;   The following allows changing a problem's SC/NSC to
 ;   NSC if there is no SC on file for patient and Problem 
 ;   original SC was set to "YES"
 ;
 I +$G(GMPORIG(1.1))=1 D
 . D BLD^DIALOG(1250000.216,$P(GMPDFN,U,2),,"MSG")
 . D EN^DDIOL(.MSG)
 ELSE  Q:'GMPSC
 S DFN=+GMPDFN D DIS^SDROUT2
 I +GMPSC=0,+$G(GMPORIG(1.1))=1 D
 . D BLD^DIALOG(1250000.217,,,"DIR(""A"")")
 . S DIR("B")="NO"
 ELSE  D
 . D BLD^DIALOG(1250000.218,,,"DIR(""A"")")
 . S:$L($G(GMPFLD(1.1))) DIR("B")=$P(GMPFLD(1.1),U,2)
 . D EN^DDIOL("")
 D BLD^DIALOG(1250000.219,,,"DIR(""?"")")
 S DIR(0)="YAO"
SC1 ;   Get Service Connection
 D ^DIR I $D(DTOUT)!(Y="^") S GMPQUIT=1 Q
 I Y?1"^".E D JUMP^GMPLEDT3(Y) Q:$D(GMPQUIT)!($G(GMPLJUMP))  K:$G(GMPIFN) GMPLJUMP G SC1
 I X="@" G:'$$SURE^GMPLX SC1 S Y=""
 S GMPFLD(1.1)=Y S:Y'="" GMPFLD(1.1)=GMPFLD(1.1)_U_$S(Y:$$EZBLD^DIALOG(1250000.188),1:$$EZBLD^DIALOG(1250000.189))
 Q
SP ; Edit Exposures/Conditions
 ;   Agent Orange - field 1.11
 ;   Ionizing Radiation - field 1.12
 ;   Persian Gulf/Environmental Contaminants - field 1.13
 ;   Head and/or Neck Cancer - field 1.15
 ;   Military Sexual Trauma - field 1.16
 ;   Combat Vet - field 1.17
 ;   SHAD - field 1.18
 G SPEXP^GMPLEDT2
 Q
SOURCE ; Edit Service - field 1.06
 ; or Clinic - field 1.08
 N DIC,X,Y,HELPMSG,PROMPT,DEFAULT,VIEW,MSG,DLG S VIEW=$E(GMPLVIEW("VIEW"))
 S DIC=$S(VIEW="S":"^DIC(49,",1:"^SC("),DIC(0)="EMQ"
 S DIC("S")="I $P(^(0),U,"_$S(VIEW="S":9,1:3)_")=""C"""
 I VIEW="S" S PROMPT=$$EZBLD^DIALOG(1250000.220),DEFAULT=$P(GMPFLD(1.06),U,2)
 E  S PROMPT=$$EZBLD^DIALOG(1250000.221),DEFAULT=$P(GMPFLD(1.08),U,2)
 S HELPMSG=$$EZBLD^DIALOG($S(VIEW="S":1250000.223,1:1250000.222))
S1 ;   Get Service/Clinic
 D EN^DDIOL(PROMPT_$S($L(DEFAULT):DEFAULT_"//",1:""))
 R X:DTIME S:'$T X="^",DTOUT=1 S:X="^" GMPQUIT=1 Q:(X="^")!(X="")
 I X?1"^".E D JUMP^GMPLEDT3(X) Q:$D(GMPQUIT)!($G(GMPLJUMP))  K:$G(GMPIFN) GMPLJUMP G S1
 I X="?" W !!,HELPMSG,! G S1
 I X["??" D @("LIST"_$S(VIEW="S":"SERV",1:"CLIN")_"^GMPLMGR1") W !,HELPMSG G S1
 I X="@" G:'$$SURE^GMPLX S1 S Y="" G SQ
 D ^DIC I Y'>0 D  G S1
 . S DLG=$S(VIEW="S":1250000.225,1:1250000.124)
 . D BLD^DIALOG(DLG,,,"MSG")
 . D EN^DDIOL(.MSG)
SQ ;   Quit Service/Clinic
 S:VIEW'="S" GMPFLD(1.08)=Y S:VIEW="S" GMPFLD(1.06)=Y
 Q
AUTHOR ; Edit Recording Provider - field 1.04
 N X,Y,PROMPT,HELPMSG,DEFAULT S PROMPT=$$EZBLD^DIALOG(1250000.226)
 S DEFAULT=$G(GMPFLD(1.04)),HELPMSG=$$EZBLD^DIALOG(1250000.227)
 D NPERSON^GMPLEDT2 Q:$D(GMPQUIT)!($G(GMPLJUMP))
 S GMPFLD(1.04)=$S(+Y>0:Y,1:"")
 Q
PROV ; Edit Responsible Provider - field 1.05
 N X,Y,PROMPT,DEFAULT,HELPMSG S DEFAULT=$G(GMPFLD(1.05))
 S PROMPT=$$EZBLD^DIALOG(1250000.228),HELPMSG=$$EZBLD^DIALOG(1250000.229)
 D NPERSON^GMPLEDT2 Q:$D(GMPQUIT)!($G(GMPLJUMP))
 S GMPFLD(1.05)=$S(+Y>0:Y,1:"")
 Q
ICD ; Edit ICD-9-CM Code - field .01
 N DIC,DIR,X,Y,MSG
ICD0 ;   Prompt for ICD Code
 K DIR S DIR(0)="FAO^2:6",DIR("A")=$$EZBLD^DIALOG(1250000.230)
 S:$P($G(GMPFLD(.01)),U,2)="799.9" DIR("A")=IORVON_$$EZBLD^DIALOG(1250000.230)
 S:+$G(GMPFLD(.01)) DIR("B")=$P(GMPFLD(.01),U,2)
 D BLD^DIALOG(1250000.231,,,"DIR(""?"")")
ICD1 ;   Get ICD Code
 D ^DIR W IORVOFF I $D(DTOUT)!(Y="^") S GMPQUIT=1 Q
 I Y?1"^".E D JUMP^GMPLEDT3(Y) Q:$D(GMPQUIT)!($G(GMPLJUMP))  K:$G(GMPIFN) GMPLJUMP G ICD1
 I X="@" D  G ICD1
 . D BLD^DIALOG(1250000.232,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 Q:X=""  Q:$P($G(GMPFLD(.01)),U,2)=Y
 S DIC=80,DIC(0)="EQM" D ^DIC G:Y'>0 ICD0
 S GMPFLD(.01)=Y
 Q
NOTE ; Attach a note to problem - field 11
 N X,Y,I,DEFAULT,PROMPT,DONE,NXT,NCNT,MSG S (I,NCNT,DONE)=0
 ; added for Code Set Versioning (CSV)
 I $G(GMPICD),'+$$STATCHK^ICDAPIU(GMPICD,DT) D  Q
 . D BLD^DIALOG(1250000.233,,,"MSG")
 . D EN^DDIOL(.MSG)
 .H 3
 I $G(GMPIFN),'$$CODESTS^GMPLX(GMPIFN,DT) D  Q
 . D BLD^DIALOG(1250000.233,,,"MSG")
 . D EN^DDIOL(.MSG)
 . H 3
 F  D  Q:$D(GMPQUIT)!($G(GMPLJUMP))!DONE
 . S NXT=$O(GMPFLD(10,"NEW",I)) S:'NXT NXT=I+1
 . S I=NXT,NCNT=NCNT+1
 . S PROMPT=$$EZBLD^DIALOG($S(NCNT=1:1250000.234,1:1250000.235)),DEFAULT=$G(GMPFLD(10,"NEW",I))
 . D EDNOTE^GMPLEDT4 Q:$D(GMPQUIT)!($G(GMPLJUMP))
 . I X="@" K GMPFLD(10,"NEW",I) Q
 . I Y="" S DONE=1 Q
 . S GMPFLD(10,"NEW",I)=Y
 Q
TERM ; Edit Problem - field 1.01
 G TERM^GMPLEDT4
 Q
Q ; No Editing
 Q
