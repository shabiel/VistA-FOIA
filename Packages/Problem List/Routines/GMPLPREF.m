GMPLPREF ; SLC/MKB -- Problem List User Preferences ;09/13/12
 ;;2.0;Problem List;**3,5,260002**;Aug 25, 1994
EN ; -- main entry point for GMPL USER PREFS
 D CURRENT^GMPLPRF0(DUZ) Q:'$$CHANGE^GMPLPRF0
 D EN^VALM("GMPL USER PREFS")
 Q
 ;
INIT ; -- init variables and list array
 N MSG
 S GMPLVIEW=$$USERVIEW^GMPLEXT(),GMPLMODE=$E(GMPLVIEW) ; 'S' or 'C'
 S GMPLMODE=$$VIEW^GMPLPRF0(GMPLMODE)
 I GMPLMODE="^" K GMPLVIEW,GMPLMODE S VALMQUIT=1 Q
 I $$ALL^GMPLPRF0(GMPLMODE,$L(GMPLVIEW,"/")) D  Q
 . D SAVE^GMPLPRF1
 . D BLD^DIALOG(1250000.357,,,"MSG")
 . D EN^DDIOL(.MSG)
 . H 1
 . S VALMQUIT=1
 D GETSLIST:GMPLMODE="S",GETCLIST:GMPLMODE'="S"
 Q
 ;
GETSLIST ; -- init SERVICE list array
 N LCNT,IFN,NAME,PARENT,CLDRN,SVCD,SVCPD,SVCS
 K ^TMP("GMPLIST",$J) S LCNT=0,^TMP("GMPLIST",$J,"VIEW",0)=0
 D EN^DDIOL($$EZBLD^DIALOG(1250000.358),,"!!")
 D SVCLIST^GMPLEXT(.SVCS,"C")
 F IFN=0:0 S IFN=$O(SVCS(IFN)) Q:IFN'>0  D
 . Q:$D(^TMP("GMPLIST",$J,"B",IFN))  ; service already on list
 . D SVCDET^GMPLEXT(.SVCD,IFN)
 . S PARENT=+$G(SVCD("PARENT"))
 . I PARENT D SVCDET^GMPLEXT(.SVCPD,PARENT)
 . I PARENT,PARENT'=IFN,$G(SVCPD("TYPE"))="C" Q  ; child of clin service
 . S NAME=$G(SVCD("NAME"))
 . D ITEM(IFN,NAME,GMPLVIEW,.LCNT)
 . D SVCCHLD^GMPLEXT(IFN,.CLDRN)
 . Q:CLDRN=0  ; service has no 'children'
 . F CHILD=0:0 S CHILD=$O(CLDRN(CHILD)) Q:CHILD'>0  I CHILD'=IFN D
 . . S NAME="  "_CLDRN(CHILD)
 . . D ITEM(CHILD,NAME,GMPLVIEW,.LCNT)
 I LCNT'>0 S ^TMP("GMPLIST",$J,1,0)="   ",^TMP("GMPLIST",$J,2,0)=$$EZBLD^DIALOG(1250000.359)
 D:$P(VALMDDF("SERVICE"),U,4)'="Service" CHGCAP^VALM("SERVICE","Service")
 S VALMCNT=LCNT,^TMP("GMPLIST",$J,0)=VALMCNT,VALMSG=$$MSG
 Q
 ;
GETCLIST ; -- init CLINIC list array
 N LCNT,IFN,NAME,LIST,DET
 K ^TMP("GMPLIST",$J) S LCNT=0,^TMP("GMPLIST",$J,"VIEW",0)=0
 D EN^DDIOL($$EZBLD^DIALOG(1250000.360),"!!")
 D CLINLST^GMPLEXT(.LIST)
 F IFN=0:0 S IFN=$O(LIST(IFN)) Q:IFN'>0  D
 . D CLINDET^GMPLEXT(.DET,IFN)
 . Q:$G(DET("TYPE"))'="C"  ; loc not a clinic
 . S NAME=$G(DET("NAME")) D ITEM(IFN,NAME,GMPLVIEW,.LCNT)
 I LCNT'>0 S ^TMP("GMPLIST",$J,1,0)="   ",^TMP("GMPLIST",$J,2,0)=$$EZBLD^DIALOG(1250000.361)
 D:$P(VALMDDF("SERVICE"),U,4)'="Clinic" CHGCAP^VALM("SERVICE","Clinic")
 S VALMCNT=LCNT,^TMP("GMPLIST",$J,0)=VALMCNT,VALMSG=$$MSG
 Q
 ;
ITEM(IFN,NAME,VIEW,CNT) ;Add item to list display
 N LNG,TMP,LINE,INCL S INCL=VIEW[("/"_IFN_"/")
 S LINE="    . . . . . . . . . . . . . . . . . . . . "
 S CNT=CNT+1,LINE=$$SETFLD^VALM1(CNT,LINE,"NUMBER")
 S LNG=4+$L(NAME),TMP=$E(LINE,1,4)_NAME_$E(LINE,LNG+1,$L(LINE)),LINE=TMP
 I INCL S LINE=$$SETFLD^VALM1(" Y",LINE,"ACCEPT"),^TMP("GMPLIST",$J,"VIEW",IFN)="",^TMP("GMPLIST",$J,"VIEW",0)=^TMP("GMPLIST",$J,"VIEW",0)+1
 S ^TMP("GMPLIST",$J,CNT,0)=LINE,^TMP("GMPLIST",$J,"IDX",CNT)=IFN,^TMP("GMPLIST",$J,"B",IFN)=CNT
 D CNTRL^VALM10(CNT,1,2,IOINHI,IOINORM) ; highlight numbers
 Q
 ;
HDR ; -- header code
 N NUM,USER,X S USER=$$PROVNAME^GMPLEXT(DUZ)
 S X="CLINIC"_$S(GMPLMODE="S":"AL SERVICE",1:"")_"S"
 S NUM=+$G(^TMP("GMPLIST",$J,"VIEW",0))_" "_$S(GMPLMODE="S":"services",1:"clinics")
 S VALMHDR(1)=USER_$J(NUM,79-$L(USER)),VALMHDR(2)=$J(X,$L(X)\2+41)
 Q
 ;
HELP ; -- help code
 N DIR,MSG
 S MSG=$S(GMPLMODE="S":1250000.362,1:1250000.363)
 S DIR(0)="EA"
 D BLD^DIALOG(MSG,,,"DIR(""A"")")
 D ^DIR
 S VALMSG=$$MSG,VALMBCK=$S(VALMCC:"",1:"R")
 Q
 ;
CLEAN ; -- exit code
 I $$DIFFRENT^GMPLPRF1,'$D(GMPSAVED) D
 . N DIR,X,Y,MSG S DIR(0)="Y"
 . D EN^DDIOL($C(7),,"?0")
 . D EN^DDIOL($$EZBLD^DIALOG(1250000.364),,"!!")
 . S DIR("A")=$$EZBLD^DIALOG(1250000.365),DIR("B")="YES"
 . S MSG=$S(GMPLMODE="S":1250000.366,1:1250000.367)
 . D BLD^DIALOG(MSG,,,"DIR(""?"")")
 . D ^DIR D:Y SAVE^GMPLPRF1
 K GMPLVIEW,GMPLIST,GMPLMODE,GMPSAVED
 K ^TMP("GMPLIST",$J)
 K VALMHDR,VALMCNT,VALMSG,VALMBCK
 Q
 ;
MSG() ; -- msg line for more help
 N X,MSG
 S MSG=$S(GMPLMODE="S":1250000.368,1:1250000.369)
 S X=$$EZBLD^DIALOG(MSG)
 Q X
