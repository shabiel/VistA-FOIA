GMPLDISP ; SLC/MKB -- Problem List detailed display ; 04/12/12
 ;;2.0;Problem List;**21,26,35,260002**;Aug 25, 1994
 ;
 ; External References
 ;   DBIA  3106  ^DIC(49
 ;   DBIA 10082  ^ICD9( file 80
 ;   DBIA 10040  ^SC(  file 44
 ;   DBIA 10060  ^VA(200
 ;   DBIA 10116  $$SETSTR^VALM1
 ;   DBIA 10117  CLEAN^VALM10
 ;   DBIA 10117  CNTRL^VALM10
 ;   DBIA 10103  $$FMTE^XLFDT
 ;   DBIA 10103  $$HTFM^XLFDT
 ;   DBIA 10104  $$REPEAT^XLFSTR
 ;                      
EN ; Init Variables (need GMPLSEL,GMPLNO) and List Array
 G:'$D(GMPLSEL) ERROR G:'$G(GMPLNO) ERROR
 N MSG
 S GMPI=+$G(GMPI)+1 I GMPI>GMPLNO D  Q
 . D BLD^DIALOG(1250000.162,,,"MSG"),EN^DDIOL(.MSG) S VALMBCK="" H 2
 S GMPLNUM=$P(GMPLSEL,",",GMPI) G:GMPLNUM'>0 ERROR
 S GMPIFN=$P($G(^TMP("GMPLIDX",$J,+GMPLNUM)),U,2) G:GMPIFN'>0 ERROR
 K MSG
 D BLD^DIALOG(1250000.163,GMPLNUM,,"MSG")
 D EN^DDIOL(.MSG)
 ;                        
PROB ; Display problem GMPIFN
 N LINE,STR,I,TEXT,NOTE,GMPL,X,Y,IDT,FAC,AIFN,SP,LCNT,NIFN,PARM
 G:'$G(GMPIFN) ERROR D CLEAN^VALM10
 S %=$$DETAILX^GMPLAPI2(.GMPL,GMPIFN,$G(GMPLMGR),$G(GMPROV))
 S SP="",LCNT=1
 F I=1:1:GMPL("EXPOSURE") D
 . S SP=SP_GMPL("EXPOSURE",I)
 . S:I'=GMPL("EXPOSURE") SP=SP_U
 D WRAP^GMPLX($$PROBTEXT^GMPLX(GMPIFN),65,.TEXT)
 S GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.151,TEXT(1))
 I TEXT>1 F I=2:1:TEXT S LCNT=LCNT+1,GMPDT(LCNT,0)=TEXT(I)
 S LCNT=LCNT+1,GMPDT(LCNT,0)="       "
PR1 ;   Onset
 ;   SC Condition
 ;   Status
 ;   Exposure
 ;   Provider
 ;   Service/Clinic
 S LINE=$$EZBLD^DIALOG(1250000.152,$S(GMPL("ONSET")'="":GMPL("ONSET"),1:$$EZBLD^DIALOG(1250000.164))),STR=""
 S:GMPVA STR=$$EZBLD^DIALOG(1250000.153,GMPL("SC"))
 S LINE=$$SETSTR^VALM1(STR,LINE,49,30),LCNT=LCNT+1,GMPDT(LCNT,0)=LINE
 S LINE=$$EZBLD^DIALOG(1250000.154,GMPL("STATUS"))
 I GMPL("PRIORITY")'="" S LINE=LINE_"/"_GMPL("PRIORITY")
 I GMPL("STATUS")="INACTIVE",GMPL("RESOLVED") S LINE=LINE_$$EZBLD^DIALOG(1250000.155,GMPL("RESOLVED"))
 S STR="",LCNT=LCNT+1
 S:GMPVA STR=$$EZBLD^DIALOG(1250000.156,$S('$L(SP):$$EZBLD^DIALOG(1250000.157),1:$P(SP,U)))
 S LINE=$$SETSTR^VALM1(STR,LINE,49,30),GMPDT(LCNT,0)=LINE
 S LINE=$$EZBLD^DIALOG(1250000.165,GMPL("PROVIDER")),LCNT=LCNT+1,STR=""
 I GMPVA,$L(SP,U)>1 S STR=$P(SP,U,2)
 S LINE=$$SETSTR^VALM1(STR,LINE,63,16),GMPDT(LCNT,0)=LINE
 I $E(GMPLVIEW("VIEW"))="S" S LINE=$$EZBLD^DIALOG(1250000.158,GMPL("SERVICE"))
 E  S LINE=$$EZBLD^DIALOG(1250000.159,GMPL("CLINIC"))
 S LCNT=LCNT+1,STR="" I GMPVA,$L(SP,U)>2 S STR=$P(SP,U,3)
 S LINE=$$SETSTR^VALM1(STR,LINE,63,16),GMPDT(LCNT,0)=LINE
 S LCNT=LCNT+1,GMPDT(LCNT,0)="       "
PR2 ;   Recorded
 ;   Entered
 ;   Provider Narrative
 ;   ICD code
 S LINE=$$EZBLD^DIALOG(1250000.161,$S($P(GMPL("RECORDED"),U):$P(GMPL("RECORDED"),U),1:$$EZBLD^DIALOG(1250000.164)))
 S:$P(GMPL("RECORDED"),U,2) LINE=LINE_$$EZBLD^DIALOG(1250000.166,$P(GMPL("RECORDED"),U,2))
 S LCNT=LCNT+1,GMPDT(LCNT,0)=LINE
 S PARM(1)=$P(GMPL("ENTERED"),U),PARM(2)=$P(GMPL("ENTERED"),U,2)
 S LINE=$$EZBLD^DIALOG(1250000.167,.PARM)
 S LCNT=LCNT+1
 S:GMPARAM("VER")&(GMPL("CONDITION")="TRANSCRIBED") LINE=LINE_$$EZBLD^DIALOG(1250000.168)
 S GMPDT(LCNT,0)=LINE
 S LINE=$$EZBLD^DIALOG(1250000.161,GMPL("DIAGNOSIS")),LCNT=LCNT+1,GMPDT(LCNT,0)=LINE
 S LCNT=LCNT+1,GMPDT(LCNT,0)="       "
PR3 ;   Comments
 S LCNT=LCNT+1,GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.169)
 D CNTRL^VALM10(LCNT,1,8,IOUON,IOUOFF)
 ;     By Facility
 I GMPL("COMMENT")=0 S LCNT=LCNT+1,GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.170) G PR4
 F I=1:1:GMPL("COMMENT") D
 . S NOTE=$P(GMPL("COMMENT",I),U,3) Q:NOTE=""
 . S LINE=$J($P(NOTE,U),10)_": "_$P(NOTE,U,3)
 . S LCNT=LCNT+1,GMPDT(LCNT,0)=LINE
 . I $P(NOTE,U,2) S LINE="            "_$P(NOTE,U,2),LCNT=LCNT+1,GMPDT(LCNT,0)=LINE
 S:'($G(NOTE)) LCNT=LCNT+1,GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.170)
PR4 ;   Audit Trail
 S LCNT=LCNT+1,GMPDT(LCNT,0)="       "
 S LCNT=LCNT+1,GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.171)
 D CNTRL^VALM10(LCNT,1,7,IOUON,IOUOFF)
 I GMPL("HISTORY")=0 S LCNT=LCNT+1,GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.172) G PRQ
 F I=1:1:GMPL("HISTORY") D 
 . S AIFN=$P(GMPL("HISTORY",I),U)
 . S %=$$AUDET^GMPLHIST(.RETURN,AIFN)
 . D DT(.GMPDT,.LCNT,.RETURN)
PRQ ;   Header Node
 S VALMCNT=LCNT,GMPDT(0)=VALMCNT,VALMSG=$$MSG^GMPLX,VALMBG=1,VALMBCK="R"
 Q
 ;                     
DT(GMPDT,LCNT,RETURN) ;
 N FLD,OLD,NEW,DATE,PROV,PAR,NEWT,OLDT
 S FLD=RETURN("FLD")
 S OLD=RETURN("OLD")
 S NEW=RETURN("NEW")
 S DATE=RETURN("DATE")
 S PROV=RETURN("PROV")
 S LCNT=LCNT+1
 I +FLD=1101 D  Q
 . S REASON=$$EZBLD^DIALOG(1250000.173)
 . S:OLD="C" REASON=$$EZBLD^DIALOG(1250000.174)
 . S PAR(1)=$J(DATE,10)
 . S PAR(2)=RETURN("OLDDATE")_REASON_PROV
 . S GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.175,.PAR)
 . S LCNT=LCNT+1,GMPDT(LCNT,0)="            "_RETURN("OLDNOTE")
 I +FLD=1.02 D  Q
 . S CHNGE=$S(NEW="H":1250000.176,OLD="T":1250000.177,1:1250000.178)
 . S PAR(1)=$J(DATE,10)
 . S PAR(2)=PROV
 . S GMPDT(LCNT,0)=$$EZBLD^DIALOG(CHNGE,.PAR)
 S PAR(1)=$J(DATE,10)
 S PAR(2)=$P(FLD,U,2)
 S PAR(3)=PROV
 S GMPDT(LCNT,0)=$$EZBLD^DIALOG(1250000.179,.PAR),LCNT=LCNT+1
 I +FLD=.12 D  Q
 . S OLDT=$S(OLD="A":$$EZBLD^DIALOG(1250000.182),OLD="I":$$EZBLD^DIALOG(1250000.183),1:$$EZBLD^DIALOG(1250000.184))
 . S NEWT=$S(NEW="A":$$EZBLD^DIALOG(1250000.182),NEW="I":$$EZBLD^DIALOG(1250000.183),1:$$EZBLD^DIALOG(1250000.184))
 . S GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.180),17)_OLDT_$$EZBLD^DIALOG(1250000.181)_NEWT
 I (+FLD=.13)!(+FLD=1.07) S GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.180),17)_OLD_$$EZBLD^DIALOG(1250000.181)_NEW Q
 I +FLD=1.14 D  Q
 . S OLDT=$S(OLD="A":$$EZBLD^DIALOG(1250000.185),OLD="C":$$EZBLD^DIALOG(1250000.186),1:$$EZBLD^DIALOG(1250000.187))
 . S NEWT=$S(NEW="A":$$EZBLD^DIALOG(1250000.185),NEW="C":$$EZBLD^DIALOG(1250000.186),1:$$EZBLD^DIALOG(1250000.187))
 . S GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.180),17)_OLDT_$$EZBLD^DIALOG(1250000.181)_NEWT
 I +FLD>1.09 D  Q
 . S OLDT=$S(+OLD:$$EZBLD^DIALOG(1250000.188),OLD=0:$$EZBLD^DIALOG(1250000.189),1:$$EZBLD^DIALOG(1250000.184))
 . S NEWT=$S(+NEW:$$EZBLD^DIALOG(1250000.188),NEW=0:$$EZBLD^DIALOG(1250000.189),1:$$EZBLD^DIALOG(1250000.184))
 . S GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.180),17)_OLDT_$$EZBLD^DIALOG(1250000.181)_NEWT
 I "^.01^.05^1.01^1.04^1.05^1.06^1.08^"[(U_+FLD_U) D
 . S GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.180),17)_$S($L($G(OLD))>0:OLD,1:$$EZBLD^DIALOG(1250000.187))
 . S LCNT=LCNT+1,GMPDT(LCNT,0)=$J($$EZBLD^DIALOG(1250000.181),17)_$S($L($G(NEW))>0:NEW,1:$$EZBLD^DIALOG(1250000.187))
 Q
 ;
HDR ; Header Code (uses GMPDFN, GMPIFN)
 N LASTMOD,PAT S PAT=$P(GMPDFN,U,2)_"  ("_$P(GMPDFN,U,3)_")"
 S:$G(GMPIFN) %=$$LASTMOD^GMPLAPI4(.LASTMOD,GMPIFN)
 S:'$G(GMPIFN) LASTMOD=$E($$HTFM^XLFDT($H),1,12)
 S LASTMOD=$$EZBLD^DIALOG(1250000.190,$$FMTE^XLFDT(LASTMOD))
 S VALMHDR(1)=PAT_$$REPEAT^XLFSTR(" ",(79-$L(PAT)-$L(LASTMOD)))_LASTMOD
 Q
 ;
HELP ; Help Code
 N DIR
 S DIR(0)="EA"
 D BLD^DIALOG(1250000.191,,,"DIR(""A"")")
 D ^DIR
 S VALMSG=$$MSG^GMPLX,VALMBCK=$S(VALMCC:"",1:"R")
 Q
 ;
DEFLT() ; Default Action, using GMPI and GMPLNO
 I GMPI<GMPLNO Q $$EZBLD^DIALOG(1250000.192)
 Q $$EZBLD^DIALOG(1250000.193)
 ;
ERROR ; Error Message - drop into EXIT
 N MSG
 D BLD^DIALOG(1250000.194,,,"MSG")
 D EN^DDIOL(.MSG)
 S VALMBCK="Q" H 1
EXIT ; Exit Code
 K GMPDT Q
