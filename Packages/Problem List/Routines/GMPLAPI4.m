GMPLAPI4 ; RGI/CBR -- Problem List API - LIST ; 09/13/12
 ;;2.0;Problem List;**260002**;Aug 25, 1994
LIST(RETURN,GMPDFN,GMPSTAT,GMPROV,GMPVIEW,GMPREV,GMPIDX,ORICD186) ;
 ; RETURN - By reference. Array of problems
 ;   RETURN = Total number of problems
 ;   RETURN(0) = No of problems returned (filtered)
 ;   RETURN(I) = Problem data: ifn^problem...
 ; GMPDFN - Patient IFN
 ; GMPSTAT - Status. Any combination of A,I,R (Active,Inactive,Removed). Default=A
 ; GMPREV - 1=Reversed. Default=0
 ; GMPROV - Filter by PROVIDER
 ; GMPVIEW - Filter. S/vamc1/vamc2/.../ or C/...
 ; GMPIDX - Create "B" index. Default=0
 N PLIST
 K RETURN
 S GMPSTAT=$G(GMPSTAT)
 S GMPREV=$G(GMPREV)
 S GMPROV=$G(GMPROV)
 S GMPVIEW=$G(GMPVIEW)
 S GMPIDX=+$G(GMPIDX)
 I '$$GETPLIST(.PLIST,GMPDFN,GMPSTAT,GMPREV,GMPROV,GMPVIEW,GMPIDX) Q 0
 D BUILDLST(.RETURN,.PLIST,$G(ORICD186))
 Q 1
 ;
GETPLIST(RETURN,GMPDFN,GMPSTAT,GMPREV,GMPROV,GMPVIEW,GMPIDX) ; lists problem ifn's
 N TOTAL
 I '+$G(GMPDFN) D ERRX^GMPLAPIE(.RETURN,"INVPARAM","GMPDFN") Q 0
 S GMPSTAT=$S($G(GMPSTAT)="B":"AI",1:GMPSTAT)
 S GMPREV=+$G(GMPREV)
 S GMPROV=+$G(GMPROV)
 S GMPVIEW=$G(GMPVIEW)
 S GMPIDX=+$G(GMPIDX)
 D GETPLIST^GMPLDAL(.RETURN,.TOTAL,GMPDFN,GMPSTAT,GMPREV,GMPROV,GMPVIEW,GMPIDX)
 S RETURN=TOTAL
 Q 1
 ;
BUILDLST(RETURN,GMPLIST,ORICD186) ; Same as LIST but returns only problems passed in GMPLIST
 N I,GMPL,IFN,INACT,ST,ICD,ONSET,LASTMOD,SC,SP,LOC,LT,PROV,SERV
 N PRIO,HASCMT,DTREC,AO,IR,ENV,HNC,MST,CV,SHD,SCCOND,TOTAL,X
 F I=1:1:GMPLIST(0) D
 . S INACT=""
 . S IFN=+GMPLIST(I)
 . Q:'$$DETAIL^GMPLDAL(IFN,.GMPL)
 . S ST=GMPL(.12)
 . I +$G(ORICD186) D
 . . S ICD=$$CODEC^ICDCODE(+GMPL(.01))
 . . I '+$$STATCHK^ICDAPIU(ICD,DT) S INACT="#"
 . E  D
 . . S ICD=$$ICDCODE^GMPLEXT(GMPL(.01))
 . S ONSET=GMPL(.13)
 . S LASTMOD=GMPL(.03)
 . S SC=$S(+GMPL(1.10):"SC",GMPL(1.10)=0:"NSC",1:"")
 . S SP=""
 . F X=1.11,1.12,1.13 S:GMPL(X) SP=SP_$S(X=1.11:"A",X=1.12:"I",1:"P")
 . S LOC=GMPL(1.08)
 . S LT=""
 . I LOC'="" S LT=$$LOCTYPE^GMPLEXT(LOC),LOC=LOC_";"_$$CLINNAME^GMPLEXT(LOC)
 . S PROV=GMPL(1.05) ; responsible provider
 . I PROV'="" S PROV=PROV_";"_$$PROVNAME^GMPLEXT(PROV)
 . S SERV=GMPL(1.06)
 . I SERV=0 S SERV="" ; not sure how it gets set to 0, but need consistency in GUI
 . I SERV'="" S SERV=SERV_";"_$$SVCNAME^GMPLEXT(SERV)
 . S PRIO=GMPL(1.14)
 . S HASCMT=$$HASNOTE^GMPLDAL3(IFN)
 . S DTREC=GMPL(1.09)
 . S AO=$S(+GMPL(1.11):"/AO",1:"")
 . S IR=$S(+GMPL(1.12):"/IR",1:"")
 . S ENV=$S(+GMPL(1.13):"/EC",1:"")
 . S HNC=$S(+GMPL(1.15):"/HNC",1:"")
 . S MST=$S(+GMPL(1.16):"/MST",1:"")
 . S CV=$S(+GMPL(1.17):"/CV",1:"")
 . S SHD=$S(+GMPL(1.18):"/SHD",1:"")
 . S SCCOND=SC_AO_IR_ENV_HNC_MST_CV_SHD
 . S RETURN(I)=IFN_U_ST_U_$$PROBTEXT^GMPLX(IFN)_U_ICD_U_ONSET
 . S RETURN(I)=RETURN(I)_U_LASTMOD_U_SC_U_SP_U_GMPL(1.02)
 . S RETURN(I)=RETURN(I)_U_LOC_U_LT_U_PROV_U_SERV_U_PRIO_U_HASCMT_U_DTREC_U_SCCOND_U_INACT
 S RETURN(0)=GMPLIST(0)
 S RETURN=+$G(GMPLIST)
 Q
 ;
FLDNAME(RETURN) ;
 N FIELDS,NAMES,I
 K RETURN
 S FIELDS=".01^.05^.12^.13^1.01^1.05^1.07^1.08^1.09^1.10^1.11^1.12^1.13^1.15^1.16^1.17^1.18"
 S NAMES="DIAGNOSIS^NARRATIVE^STATUS^ONSET^LEXICON^PROVIDER^RESOLVED^LOCATION^RECORDED^SC^AO^IR^EC^HNC^MST^CV^SHD"
 F I=1:1:$L(FIELDS,"^") S RETURN($P(FIELDS,"^",I))=$P(NAMES,"^",I)
 Q
 ;
LASTMOD(RETURN,GMPIFN) ; last modified date
 S RETURN=$$LASTMOD^GMPLDAL3(GMPIFN)
 Q 1
 ;
DEFAULT(RETURN,GMPROB,GMPICD,GMPTERM,GMPROV,GMPCLIN,GMPELIG) ;
 I '+$$STATCHK^ICDAPIU(GMPICD,DT) D ERRX^GMPLAPIE(.RETURN,"INACTICD") Q 0
 S RETURN(.01)=$$ICD9KEY^GMPLEXT(GMPICD)_U_GMPICD
 S:'RETURN(.01) RETURN(.01)=$$NOS^GMPLEXT ; cannot resolve code
 S RETURN(.05)=U_GMPROB
 S RETURN(.08)=DT_U_$$EXTDT^GMPLX(DT)
 S RETURN(.12)="A^ACTIVE"
 S RETURN(.13)=""
 S RETURN(1.01)=GMPTERM
 S RETURN(1.02)=$S('$$VERIFY^GMPLSITE:"P",$$CLINUSER^GMPLEXT:"P",1:"T")
 S RETURN(1.03)=DUZ
 S RETURN(1.04)=$G(GMPROV)
 S RETURN(1.05)=$G(GMPROV)
 S RETURN(1.06)=$$SERVICE^GMPLEXT(+$G(GMPROV))
 S RETURN(1.07)=""
 S RETURN(1.08)=$G(GMPCLIN)
 S RETURN(1.09)=DT_U_$$EXTDT^GMPLX(DT)
 S RETURN(1.14)=""
 S RETURN(1.10)=$S('$D(GMPELIG("SC")):"0^NO",1:"")
 S RETURN(1.11)=$S('$D(GMPELIG("AO")):"0^NO",1:"")
 S RETURN(1.12)=$S('$D(GMPELIG("IR")):"0^NO",1:"")
 S RETURN(1.13)=$S('$D(GMPELIG("EC")):"0^NO",1:"")
 S RETURN(10,0)=0
 Q 1
 ;
UNDELETE(RETURN,GMPIFN) ; -- replace problem on patient's list
 N DELETED,%
 S RETURN=0
 S %=$$DELETED^GMPLAPI2(.DELETED,GMPIFN)
 I 'DELETED D ERRX^GMPLAPIE(.RETURN,"INVREC") Q 0
 D CHGCOND^GMPLDAL(GMPIFN,"H","P")
 S RETURN=1
 Q 1
 ;
DIAG(RETURN,GMPIFN) ; Returns ICD diagnosis: pointer_to_icd_file^icd
 N ICD,DET,%
 S RETURN=""
 Q:'+$G(GMPIFN) 0
 S %=$$DETAIL^GMPLAPI2(.DET,GMPIFN)
 S ICD=$P(DET(.01),U)
 S RETURN=ICD_U_$$ICDCODE^GMPLEXT(ICD)
 Q 1
 ;
PROBNARR(RETURN,GMPIFN) ; Returns Provider Narrative
 N PRB,DET,%
 S RETURN=""
 Q:'+$G(GMPIFN) 0
 S %=$$DETAIL^GMPLAPI2(.DET,GMPIFN)
 S PRB=+DET(.05)
 S RETURN=PRB_U_$$NARR^GMPLEXT(PRB)
 Q 1
 ;
PRBCNT(RETURN) ;Return number of entries in PROBLEM LIST.
 S RETURN=$$PRBCNT^GMPLDAL3()
 Q 1
 ;
VALID(RETURN,GMPIFN) ;RETURN=1 if problem IFN is valid
 S RETURN=$$EXISTS^GMPLDAL(+$G(GMPIFN))
 Q 1
 ;
PATIENT(RETURN,GMPIFN) ; Returns patient IFN given problem IFN
 N VALID,%,DET
 S RETURN=0
 Q:'+$G(GMPIFN) 0
 S %=$$VALID(.VALID,GMPIFN)
 Q:'VALID 1
 S %=$$DETAIL^GMPLAPI2(.DET,GMPIFN)
 S RETURN=+DET(.02)
 Q 1
 ;
REPLACE(RETURN,GMPIFN,NEWDIAG) ; Replace ICD diagnosis code
 N OLDDIAG,%
 S RETURN=0
 Q:$G(NEWDIAG)="" 0
 S %=$$DIAG(.OLDDIAG,GMPIFN)
 S OLDDIAG=$P(OLDDIAG,U)
 S NEWDIAG=$P(NEWDIAG,U)
 Q:OLDDIAG=NEWDIAG 0
 D REPLACE^GMPLDAL(GMPIFN,OLDDIAG,NEWDIAG)
 S RETURN=1
 Q 1
 ;
HASPRBS(RETURN,GMPDFN,GMPSTAT) ; Returns 1 if patient DFN has problems with status GMPSTAT
 ; GMPSTAT=A/I/AI; Default: AI
 S GMPSTAT=$G(GMPSTAT,"AI")
 S RETURN=0
 Q:'+$G(GMPDFN) 0
 S RETURN=$$HASPRBS^GMPLDAL3(GMPDFN,GMPSTAT)
 Q 1
MODIFIED(RETURN,GMPDFN) ; Return the Date the Patients Problem List was Last Modified
 S RETURN=$$MODIFIED^GMPLDAL3(GMPDFN)
 Q 1
