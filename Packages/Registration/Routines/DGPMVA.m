DGPMVA ;RGI/VSL - ADMIT PATIENT; 12/14/2012
 ;;5.3;Registration;**260003**;
ADMIT ;
 N DFN,ADMS
 S DFN=$$SELPAT() Q:'DFN
 D MEANST^DGPMVN(DFN)
 D PATSTAT^DGPMVN(DFN,1)
 S %=$$CONTINUE^DGPMVN()
 G:%="Q"!(%="") ADMIT
ADMIT1 ;
 N RE,ERR
 S %=$$LSTPADMS^DGPMAPI7(.ADMS,DFN)
 S AFN=$$SELADM(.ADMS,.NEW)
 G:AFN'>0 ADMIT
 I NEW D NEWADM(.RE,AFN,DFN)
 E  D UPDADM(.RE,AFN)
 I RE=0 D
 . I $P(RE(0),U)="FILELOCK" D BLD^DIALOG(4070000.083,,,"ERR")
 . E  S ERR(1)=$P(RE(0),U,2)
 . D EN^DDIOL(.ERR)
 G ADMIT
 Q
 ;
UPDADM(RETURN,AFN) ;
 N OADM,NADM,DELETED
 S RETURN=1
 S %=$$GETADM^DGPMAPI8(.OADM,AFN)
UPD ;
 S %=$$SELDATE(OADM("DATE")) Q:%="^"
 I %="@" S DELETED=$$DELADM(AFN)
 I DELETED Q
 E  G:'DELETED UPD
 S NADM("DATE")=%
 D SELALL(.NADM,.OADM,OADM("PATIENT"))
 S %=$$UPDADM^DGPMAPI1(.RETURN,.NADM,AFN)
 Q
 ;
DELADM(AFN) ; Delete admission
 N %,TXT,PARAM,DEFAULT,RETURN,ERR
 S TXT(1)="ADMISSION"
 S PARAM("PROMPT")=4070000.084,PARAM("HELPT")=$$EZBLD^DIALOG(4070000.085,.TXT)
 S DEFAULT=2
 S %=$$CANDEL^DGPMAPI1(.ERR,AFN)
 I ERR=0 D EN^DDIOL($P(ERR(0),U,2)) Q 0
 S %=$$YN^DGPMUTL1(.PARAM,.DEFAULT)
 Q:%'=1 0
 S %=$$DELADM^DGPMAPI1(.RETURN,AFN)
 I RETURN=0 D EN^DDIOL($P(RETURN(0),U,2)) Q 0
 Q 1
 ;
SELDATE(DGDT) ;
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.076))
 S PAR("HELP1")=4070000.077,PAR("HELP2")=4070000.077
 S PAR("VALID")="VALDT^DGPMVA"
 Q $$SELTEXT^DGPMUTL1(.PAR,$P(DGDT,U,2))
 ;
VALDT(DGDT,VAL) ; Is selected date valid
 N %DT,Y,X
 S VAL=0,X=DGDT
 I DGDT="@"!(DGDT="^") S VAL=-1 Q
 S %DT="SRXE",%DT(0)="-NOW"
 D ^%DT I Y<0 Q
 S VAL=Y
 Q
SELADM(ADMS,NEW) ;
 N I,LN,LNO,ADMDT,TMP,DDT,X,Y,DIR S LNO=0,NEW=1
SELADM1 ;
 I +$G(ADMS(0))>0,'$D(TMP) D
 . D EN^DDIOL($$UP^XLFSTR($$EZBLD^DIALOG(8068)))
 . F I=0:0 S I=$O(ADMS(I)) Q:I=""  D
 . . S LNO=LNO+1,TMP(LNO)=ADMS(I,"ID"),TMP(+ADMS(I,"DATE"))=ADMS(I,"ID")
 . . I ADMS(I,"DISCH")="" S NEW=0
 . . S LN(LNO)=LNO_"> "_$P(ADMS(I,"DATE"),U,2)_$C(9)_$P(ADMS(I,"TYPE"),U,2)
 . . S LN(LNO)=LN(LNO)_$C(9)_" TO: "_$P(ADMS(I,"WARD"),U,2)_" ["_$P(ADMS(I,"BED"),U,2)_"]"
 . . I '$D(ADMDT) S ADMDT=ADMS(I,"DATE")
 . . I +ADMDT<+ADMS(I,"DATE") S ADMDT=ADMS(I,"DATE")
 . D EN^DDIOL(.LN)
 I '$D(ADMDT) S ADMDT="^"_$$EZBLD^DIALOG(37007)
 S DDT=$S(NEW:$$EZBLD^DIALOG(37007),1:$P(ADMDT,U,2))
 S DIR(0)="FAO^"
 S DIR("A")=$$EZBLD^DIALOG(4070000.003)_" "_DDT_"//"
 D ^DIR
 I $G(TMP(+Y))>0 S NEW=0 Q TMP(+Y)
 I Y="" S Y=DDT
 S X=Y,%DT="SEXT",%DT(0)="-NOW" D ^%DT I $G(TMP(+Y)) Q TMP(+Y)
 Q:NEW!(Y<0) Y
 G SELADM1
 Q 0
 ;
SELPAT() ; Select patient
 N PAR,DFN,MT
 S PAR("PROMPT")=$$EZBLD^DIALOG(4070000.004),PAR("ENTITY")=$$EZBLD^DIALOG(4070000.005)
 S PAR("RTN")="LSTPATS^DGPMVA"
 S PAR("FLDS")="NAME;BIRTH;SSN;VETERAN;TYPE"
 S PAR("HELP1")=4070000.006
 S DFN=$$SELECT^DGPMUTL1(.PAR)
 S:+DFN>0 MT=$$CMTS^DGMTU(DFN)
 Q DFN
 ;
NEWADM(RETURN,AFN,DFN) ;
 N %,ADM,RE,DIAG
 S %=$$ASK(AFN)
 I %'=1 G ADMIT1
 S ADM("DATE")=AFN
 S ADM("PATIENT")=DFN
 D SELALL(.ADM,,DFN)
 S %=$$ADMIT^DGPMAPI1(.RETURN,.ADM)
 Q %
 ;
SELALL(ADM,OADM,DFN) ;
 N RE,DIAG
 S ADM("FDEXC")=$$FDEXC($G(OADM("FDEXC")))
 S ADM("ADMREG")=$$SELADREG($G(OADM("ADMREG")))
 S ADM("ADMSCC")=$$SCCOND($G(OADM("ADMSCC")))
 S ADM("TYPE")=$$SELTYPE($G(OADM("TYPE")))
 S ADM("SHDIAG")=$$DIAG($P($G(OADM("SHDIAG")),U))
 S ADM("WARD")=$$SELWARD($G(OADM("WARD")))
 S ADM("ROOMBED")=$$SELBED(ADM("WARD"),$G(OADM("ROOMBED")))
 S ADM("FTSPEC")=$$SELFTS(ADM("DATE"),$G(OADM("FTSPEC")))
 S ADM("PRYMPHY")=$$SELPPROV(ADM("DATE"),$G(OADM("PRYMPHY")))
 S ADM("ATNDPHY")=$$SELAPROV(ADM("DATE"),$G(OADM("ATNDPHY")))
 S DIAG(1)=ADM("SHDIAG") M:$D(OADM("DIAG")) DIAG=OADM("DIAG")
 D EDITDIAG(.DIAG,ADM("DATE"))
 M ADM("DIAG")=DIAG
 S ADM("ADMSRC")=$$SELSRC($G(OADM("ADMSRC")))
 S ADM("ELIGIB")=$$ELIG^DGUTL3(+DFN,1,$P($G(OADM("ELIGIB")),U))
 S ADM("SERILL")=$$SERILL($G(OADM("SERILL"),"S^SERIOUSLY ILL"))
 Q
ASK(AFN) ;
 N %,DIR,TXT G ASK1
 Q %
ASK1 ;
 S TXT(1)=$$FMTE^XLFDT(AFN)
 D:$D(%) EN^DDIOL($$EZBLD^DIALOG(4070000.002))
 D EN^DDIOL($$EZBLD^DIALOG(4070000.001,.TXT))
 S %=3 D YN^DICN I '% G ASK1
 Q %
 ;
FDEXC(DEFAULT) ; Facility directory exclusion
 N PAR,LIST
 S PAR("FLDS")="ID;NAME;",PAR("FLAG")="R"
 S LIST(1,"ID")=1,LIST(1,"NAME")="YES"
 S LIST(2,"ID")=0,LIST(2,"NAME")="NO",LIST(0)=2
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.007)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("HELP1")=4070000.008,PAR("HELP2")=4070000.009
 Q $$SELECT^DGPMUTL1(.PAR,.LIST,DEFAULT)
 ;
SELADREG(DEFAULT) ; Select admitting regulation
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.01)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTADREG^DGPMVA",PAR("FLDS")="ID;NAME;CFR;",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.011,PAR("HELP2")=4070000.012,PAR("HELP3")=4070000.013
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SCCOND(DEFAULT) ; Admitted for sc condition?
 N PAR,LIST
 S PAR("FLDS")="ID;NAME;"
 S LIST(1,"ID")=1,LIST(1,"NAME")="YES"
 S LIST(2,"ID")=0,LIST(2,"NAME")="NO",LIST(0)=2
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.014)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("HELP1")=4070000.015,PAR("HELP2")=4070000.016
 Q $$SELECT^DGPMUTL1(.PAR,.LIST,DEFAULT)
 ;
SELTYPE(DEFAULT) ; Select admission type
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.018)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTADTYP^DGPMVA",PAR("FLDS")="ID;NAME;TYPE;STATUS",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.019,PAR("HELP2")=4070000.02,PAR("HELP3")=4070000.021
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
DIAG(DEFAULT) ; Get short diagnosis
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.022)),PAR("FLAG")="R"
 S PAR("HELP1")=4070000.023,PAR("HELP2")=4070000.024
 S PAR("MIN")=3,PAR("MAX")=30
 Q $$SELTEXT^DGPMUTL1(.PAR,DEFAULT)
 ;
SELWARD(DEFAULT) ; Select ward
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.025)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTWARD^DGPMVA",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.026,PAR("HELP2")=4070000.027,PAR("HELP3")=4070000.028
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELBED(WARD,DEFAULT) ; Select bed
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.029)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTWBED^DGPMVA",PAR("FLDS")="NAME;DESC"
 S PAR("HELP1")=4070000.03,PAR("HELP2")=4070000.031
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELFTS(DGDT,DEFAULT) ; Select facility treating specialty
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.032)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTFTS^DGPMVA",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.033,PAR("HELP2")=4070000.034,PAR("HELP3")=4070000.035
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELPPROV(DGDT,DEFAULT) ; Select primary physician
 N PAR S PAR("FLDS")="NAME;NAME;INITIAL;TITLE"
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.036)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTPROV^DGPMVA",PAR("HELP1")=4070000.037,PAR("HELP2")=4070000.038
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELAPROV(DGDT,DEFAULT) ; Select attending physician
 N PAR S PAR("FLDS")="NAME;NAME;INITIAL;TITLE",PAR("FLAG")="R"
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.039)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTPROV^DGPMVA",PAR("HELP1")=4070000.04,PAR("HELP2")=4070000.041
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
EDITDIAG(DIAG,AFN) ; Edit text
 N DIC,DWPK K ^UTILITY($J,"DIAG",AFN)
 D EN^DDIOL($$UP^XLFSTR($$EZBLD^DIALOG(4070000.042))_":")
 F I=0:0 S I=$O(DIAG(I)) Q:I=""  S ^UTILITY($J,"DIAG",AFN,I,0)=DIAG(I)
 K DIAG
 S DIC="^UTILITY($J,""DIAG"","_AFN_",",DWPK=1
 D EN^DIWE
 F I=0:0 S I=$O(^UTILITY($J,"DIAG",AFN,I)) Q:I=""  S DIAG(I)=^UTILITY($J,"DIAG",AFN,I,0)
 K ^UTILITY($J,"DIAG",AFN)
 Q
SELSRC(DEFAULT) ; Select admission source
 N PAR S PAR("FLDS")="CODE;NAME;PLACE"
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.043)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTADSRC^DGPMVA",PAR("HELP1")=4070000.044
 S PAR("HELP2")=4070000.045,PAR("HELP3")=4070000.046
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SERILL(DEFAULT) ; Seriously ill
 N PAR,LIST
 S PAR("FLDS")="ID;NAME;"
 S LIST(1,"ID")="S",LIST(1,"NAME")="SERIOUSLY ILL",LIST(0)=1
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.047)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("HELP1")=4070000.048,PAR("HELP2")=4070000.049
 Q $$SELECT^DGPMUTL1(.PAR,.LIST,DEFAULT)
 ;
LSTPATS(RETURN,SEARCH,START,NUMBER) ; Lists patients
 S %=$$LSTPATS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTADREG(RETURN,SEARCH,START,NUMBER) ; Lists admitting regulation
 S %=$$LSTADREG^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTADTYP(RETURN,SEARCH,START,NUMBER) ; Lists movement types
 S %=$$LSTADTYP^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTWARD(RETURN,SEARCH,START,NUMBER) ; Lists wards
 S %=$$LSTWARD^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTWBED(RETURN,SEARCH,START,NUMBER) ; Lists beds
 S %=$$LSTWBED^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,.WARD,.DFN)
 Q
LSTFTS(RETURN,SEARCH,START,NUMBER) ; Lists facility treating specialties
 S %=$$LSTFTS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,.DGDT)
 Q
LSTPROV(RETURN,SEARCH,START,NUMBER) ; Lists active providers
 S %=$$LSTPROV^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,.DGDT)
 Q
LSTADSRC(RETURN,SEARCH,START,NUMBER) ; Lists admitting regulation
 S %=$$LSTADSRC^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
