DGPMVN ;RGI/VSL - PATIENT MOVEMENT UTILS; 2/20/2013
 ;;5.3;Registration;**260005**;
 ;
SELPAT(RTN,PROMPT) ; Select patient
 N PAR,DFN,MT,TXT,PAT
 S PAR("PROMPT")=$$EZBLD^DIALOG(PROMPT),PAR("ENTITY")=$$EZBLD^DIALOG(4070000.005)
 S PAR("RTN")=RTN
 S PAR("FLDS")="NAME;BIRTH;SSN;VETERAN;TYPE"
 S PAR("HELP1")=4070000.006
 S DFN=$$SELECT^DGPMUTL1(.PAR)
 S:+DFN>0 MT=$$CMTS^DGMTU(DFN)
 S:+DFN>0 %=$$GETPAT^DGPMAPI8(.PAT,DFN)
 I +$G(PAT("DTHDT"))>0 S TXT(1)=$P(PAT("DTHDT"),U,2),%=$$ASK(4070000.117,4070000.118,2,.TXT) Q:%=2 -1
 Q DFN
 ;
MEANST(DFN) ; Display means test
 N DOM
 S %=$$GETPAT^DGPMAPI8(.PAT,DFN)
 I $P(PAT("MEANST"),U) D
 . S %=$$ISDOM^DGPMAPI8(.DOM,DFN)
 . D:'DOM DIS^DGMTU(DFN)
 E  D EN^DDIOL($$EZBLD^DIALOG(4070000.089))
 Q
 ;
CONTINUE() ; Continue
 N PAR,LIST
 S PAR("FLDS")="TXT;",PAR("SRCHFLDS")="ID;NAME"
 S LIST(1,"ID")="C",LIST(1,"NAME")="CONTINUE",LIST(1,"TXT")=$$EZBLD^DIALOG(4070000.073)
 S LIST(2,"ID")="M",LIST(2,"NAME")="MORE",LIST(2,"TXT")=$$EZBLD^DIALOG(4070000.074)
 S LIST(3,"ID")="Q",LIST(3,"NAME")="QUIT",LIST(3,"TXT")=$$EZBLD^DIALOG(4070000.075),LIST(0)=3
 S PAR("PROMPT")=$$EZBLD^DIALOG(4070000.072)
 Q $$SELECT^DGPMUTL1(.PAR,.LIST,"C^CONTINUE")
 ;
PATSTAT(DFN,DET) ; Display patient status
 N %,STAT,PAT,CNT,ELIG,LMVT,ADM,ADMN,ATD,BED,INP,LST,MVTN,MVTDT
 N PRV,SPC,STAT1,STAT2,STAT3,WRD,STATUS
 D PCMM^SCRPU4(DFN,DT)
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S STATUS=$$EZBLD^DIALOG(4070000.05)
 I $D(LMVT)'>1 D
 . S CNT=1
 . S STAT=$$EZBLD^DIALOG(4070000.051)
 . S LST(CNT)=$$GETLN(STATUS,STAT)
 E  D
 . S CNT=1
 . I LMVT("TYPE")<4 S INP=$$EZBLD^DIALOG(4070000.066),ADMN=$$EZBLD^DIALOG(4070000.056)
 . E  S INP=$$EZBLD^DIALOG(4070000.067),ADMN=$$EZBLD^DIALOG(4070000.07)
 . I +LMVT("TYPE")=3!(+LMVT("TYPE")=5) D
 . . S STAT1=$$EZBLD^DIALOG(4070000.064)
 . . S:+LMVT("TYPE")=3 STAT2=$$EZBLD^DIALOG(4070000.068),STAT3=$P(LMVT("MTYPE"),U,2)
 . E  D
 . . S STAT1=$$EZBLD^DIALOG(4070000.065)
 . . S:$P(LMVT("SERILL"),U,1)="S" INP=INP_" ("_$P(LMVT("SERILL"),U,2)_")"
 . S LST(CNT)=$$GETLN(STATUS,STAT1_" "_INP,.STAT2,.STAT3)
 . S CNT=CNT+1,LST(CNT)=$S(+LMVT("FDEXC"):$$EZBLD^DIALOG(4070000.069),1:"")
 . S ADM=$P(LMVT("ADMDT"),U,2)
 . S:+LMVT("TYPE")=2!(+LMVT("TYPE")=1) MVTN=$$EZBLD^DIALOG(4070000.057)
 . S:+LMVT("TYPE")=2 MVTDT=$P(LMVT("DATE"),U,2)
 . S:+LMVT("TYPE")=3 MVTN=$$EZBLD^DIALOG(4070000.058),MVTDT=$P(LMVT("DATE"),U,2)
 . S:+LMVT("TYPE")>3 MVTN=$$EZBLD^DIALOG(4070000.071)
 . S:+LMVT("TYPE")=5 MVTDT=$P(LMVT("DATE"),U,2)
 . S CNT=CNT+1,LST(CNT)=$$GETLN(ADMN,ADM,.MVTN,.MVTDT)
 . S WRD=$P(LMVT("WARD"),U,2),BED=$P(LMVT("ROOMBED"),U,2)
 . S CNT=CNT+1,LST(CNT)=$$GETLN($$EZBLD^DIALOG(4070000.059),WRD,$$EZBLD^DIALOG(4070000.06),BED)
 . S PRV=$P(LMVT("PRYMPHY"),U,2),SPC=$P(LMVT("FTSPEC"),U,2)
 . S CNT=CNT+1,LST(CNT)=$$GETLN($$EZBLD^DIALOG(4070000.061),PRV,$$EZBLD^DIALOG(4070000.062),SPC)
 . S:+LMVT("TYPE")>3 CNT=CNT-1
 . S ATD=$P(LMVT("ATNDPHY"),U,2)
 . S CNT=CNT+1,LST(CNT)=$$GETLN($$EZBLD^DIALOG(4070000.063),ATD)
 D EN^DDIOL(.LST)
 I $G(DET)=1 D PATDET(DFN)
 Q
 ;
SELMVT(MVTS,NEW,PROMPT) ;
 N %DT,I,LN,LNO,ADMDT,TMP,DDT,X,Y,DIR S LNO=0,NEW=1
SELMVT1 ;
 I +$G(MVTS(0))>0,'$D(TMP) D
 . F I=0:0 S I=$O(MVTS(I)) Q:I=""  D
 . . I MVTS(I,"TTYPE")=1,MVTS(I,"DISCH")="" S NEW=0
 . . S:LNO=0 LN(LNO)=$$UP^XLFSTR($$EZBLD^DIALOG(8068))
 . . S LNO=LNO+1,TMP(LNO)=MVTS(I,"ID"),TMP(+MVTS(I,"DATE"))=MVTS(I,"ID")
 . . S LN(LNO)=LNO_"> "_$P(MVTS(I,"DATE"),U,2)_$C(9)_$P(MVTS(I,"TYPE"),U,2)
 . . S LN(LNO)=LN(LNO)_$C(9)_" TO: "_$P(MVTS(I,"WARD"),U,2)_" ["_$P(MVTS(I,"ROOMBED"),U,2)_"]"
 . . I '$D(ADMDT) S ADMDT=MVTS(I,"DATE")
 . . I +ADMDT<+MVTS(I,"DATE") S ADMDT=MVTS(I,"DATE")
 . D EN^DDIOL(.LN)
 I '$D(ADMDT) S ADMDT="^"_$$EZBLD^DIALOG(37007)
 S DDT=$S(NEW:$$EZBLD^DIALOG(37007),1:$P(ADMDT,U,2))
 S DIR(0)="FO^"
 S DIR("A")=$$EZBLD^DIALOG(PROMPT),DIR("B")=DDT
 D ^DIR
 I $G(TMP(+Y))>0 S NEW=0 Q TMP(+Y)
 I Y="" S Y=DDT
 S X=Y,%DT="SEXT",%DT(0)="-NOW" D ^%DT I $G(TMP(+Y)) Q TMP(+Y)
 Q:Y>0 Y
 Q:NEW&(X=""!(X="^")) Y
 G SELMVT1
 Q 0
 ;
PATDET(DFN) ;
 N PAT,LST
 S %=$$GETPAT^DGPMAPI8(.PAT,DFN),LST(1)=""
 S LST(2)=$$GETLN($$EZBLD^DIALOG(4070000.052),$P(PAT("RELIG"),U,2),$$EZBLD^DIALOG(4070000.053),$P(PAT("MSTAT"),U,2))
 S ELIG=$P(PAT("ELIG"),U,2)_" ("_$S(PAT("ESTAT")="^":$$EZBLD^DIALOG(4070000.055),1:$P(PAT("ESTAT"),U,2))_")"
 S LST(3)=$$GETLN($$EZBLD^DIALOG(4070000.054),ELIG),LST(4)=""
 D EN^DDIOL(.LST)
 Q
GETLN(T1,T2,T3,T4) ; Format line
 N TXT,C1,C3
 S T1=$G(T1),T2=$G(T2),T3=$G(T3),T4=$G(T4)
 S C1="             ",C3=C1_C1_C1
 S TXT=T1_$E(C1,$L(T1),$L(C1)-2)_": "_T2
 Q:T3="" TXT
 S TXT=TXT_$E(C3,$L(TXT),$L(C3)-1)
 S TXT=TXT_T3_$E(C1,$L(T3),$L(C1)+4)_": "_T4
 Q TXT
ASK(PRMT,HELP,DEFAULT,TXT) ;
 N %,DIR G ASK1
 Q %
ASK1 ;
 D:$D(%) EN^DDIOL($$EZBLD^DIALOG(HELP))
 D EN^DDIOL($$EZBLD^DIALOG(PRMT,.TXT))
 S %=$S($D(DEFAULT):DEFAULT,1:3) D YN^DICN I '% G ASK1
 Q %
 ;
