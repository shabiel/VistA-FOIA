DGPMAPI3 ;RGI/VSL - DISCHARGE PATIENT API; 3/6/2013
 ;;5.3;Registration;**260005**;
CHKDT(RETURN,PARAM,TYPE,MAS,ADM) ; Check discharge date
 N %,TXT,TT,DIS
 K RETURN S RETURN=0 ; patient
 D GETMVT^DGPMDAL1(.ADM,+$G(PARAM("ADMIFN")),".02;.03;")
 D GETMVTT^DGPMDAL2(.TT,+$G(PARAM("TYPE")))
 S TYPE=$G(TT(.03,"I"))
 D GETMASMT^DGPMDAL2(.MAS,TYPE)
 I $G(PARAM("ADMIFN"))="" S TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETMVT^DGPMDAL1(.ADM,+$G(PARAM("ADMIFN")))
 I ADM=0 S TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"ADMNFND",.TXT) Q 0
 I ADM(.17,"I")>0 D  Q 0
 . D GETMVT^DGPMDAL1(.DIS,+ADM(.17,"I"))
 . S TXT(1)=DIS(.01,"E")
 . D ERRX^DGPMAPIE(.RETURN,"DCHPADON",.TXT)
 I $G(PARAM("DATE"))=""!(+$G(PARAM("DATE"))<1800000) D  Q 0
 . S TXT(1)="PARAM('DATE')",RETURN=0 D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 I $$TIMEUSD^DGPMDAL2(+ADM(.03,"I"),+PARAM("DATE")) D ERRX^DGPMAPIE(.RETURN,"TIMEUSD") Q 0
 I +PARAM("DATE")<ADM(.01,"I") D ERRX^DGPMAPIE(.RETURN,"TRANBADM",.TXT) Q 0
 S %=$$GETLASTM^DGPMAPI8(.LMVT,+ADM(.03,"I"))
 I +PARAM("DATE")<LMVT("DATE") D ERRX^DGPMAPIE(.RETURN,"DCHNBLM",.TXT) Q 0
 S RETURN=1
 Q 1
CHKDSCH(RETURN,PARAM,TYPE,MAS,ADM) ; Check discharge patient
 N %,TXT,TT,ADM,DIS
 K RETURN S RETURN=0 ; patient
 S %=$$CHKDT(.RETURN,.PARAM,.TYPE,.MAS,.ADM)
 ; type of movement
 S PARAM("PATIENT")=+ADM(.03,"I")
 S %=$$CHKDTYP(.RETURN,$G(PARAM("TYPE")),+ADM(.03,"I"),+PARAM("DATE")) Q:'RETURN 0
 D GETMVT^DGPMDAL1(.ADM,+$G(PARAM("ADMIFN")),".02;.03;")
 D GETMVTT^DGPMDAL2(.TT,+$G(PARAM("TYPE")))
 S TYPE=$G(TT(.03,"I"))
 I +TYPE=46 I $$GETAMT^DGPMDAL3(45)="" S TXT(1)=MAS(.01,"E") D ERRX^DGPMAPIE(.RETURN,"DSCNMVTD",.TXT) Q 0
 S RETURN=1
 Q 1
CHKDTYP(RETURN,TYPE,DFN,DATE) ; Check discharge type
 N %,TMP,TXT,ADTYP,ERR K RETURN S RETURN=0
 S %=$$CHKTYPE^DGPMAPI8(.RETURN,.TYPE,+DFN,+DATE,.TMP) Q:'RETURN 0
 S RETURN=0
 S %=$$LSTDTYP^DGPMAPI7(.ADTYP,+TYPE,,,+DFN,+DATE)
 S TXT(1)=TMP(.01,"E")
 I $G(TMP(.04,"I"))'=1 D ERRX^DGPMAPIE(.RETURN,"MVTTINAC",.TXT) Q 0
 I +$G(ADTYP(0))=0 D ERRX^DGPMAPIE(.RETURN,"DISINVAT",.TXT) Q 0
 S RETURN=1
 Q 1
 ;
DISCH(RETURN,PARAM) ; Discharge patient
 ;
 ;Input
 ;
 ;Required elements include:
 ;  PARAM("DATE")=date time (discharge date)
 ;  PARAM("TYPE")=type of movement
 ;  PARAM("ADMIFN")=admission ifn
 N %,PM,APM,DFN,IFN,ADM,WARD,ADMIFN,DATE,PAT,DGDT,IFNP,MAS,PTF,RPTF
 N TFCTY,TT,TXT
 S %=$$CHKDSCH(.RETURN,.PARAM,.TYPE,.MAS,.ADM)
 I %=0 Q 0
 S DFN=+PARAM("PATIENT")
 S %=$$LOCKMVT^DGPMDAL1(DFN)
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 D INITEVT^DGPMDAL1
 D SETAEVT^DGPMDAL1(+PARAM("ADMIFN"),,"A")
 I +TYPE=46 S TFCTY=+PARAM("FCTY") K PARAM("FCTY")
 I +TYPE=41 S %=$$DELASH(.PARAM,.MAS,TYPE)
 S IFN=$$ADDDIS(.RETURN,.PARAM,DFN,TYPE)
 I +TYPE=46!(+TYPE=41) S PARAM("FCTY")=$G(TFCTY),%=$$ASIH(.PARAM,.MAS,TYPE)
 S DGPMDA=IFN D DIS^DGPMVODS
 S %=$$UPDPAT^DGPMAPI7(,,DFN)
 D SETDEVT^DGPMDAL1(IFN,"A")
 D MVTEVT^DGPMAPI7(DFN,3,IFN)
 D ULOCKMVT^DGPMDAL1(DFN)
 Q 1
 ;
DELASH(PARAM,MAS,TYPE) ; Delete ASIH discharge
 N %,ASH,MVT,ADM,FLD,ADM1
 S FLD=".01;.03;.06;.14;.15;.16;.17;.21;"
 D GETMVT^DGPMDAL1(.ASH,+PARAM("ADMIFN"),FLD)
 D GETMVT^DGPMDAL1(.MVT,ASH(.21,"I"),FLD)
 D GETMVT^DGPMDAL1(.ADM,MVT(.14,"I"),FLD)
 I +TYPE=41 D
 . D DELMVT^DGPMDAL1(ADM(.17,"I"))
 S ADM1(.17)=""
 D UPDMVT^DGPMDAL1(,.ADM1,MVT(.14,"I"))
 S PTF(70)="@",PTF(71)="@",PTF(72)="@"
 D UPDPTF^DGPMDAL1(,.PTF,ADM(.16,"I"))
 Q %
 ;
ASIH(PARAM,MAS,TYPE) ; ASIH discharge
 N %,ASH,MVT,ADM,TRMAS,FLD,TRA
 S FLD=".01;.03;.06;.14;.15;.16;.17;.21;"
 D GETMVT^DGPMDAL1(.ASH,+PARAM("ADMIFN"),FLD)
 D GETMVT^DGPMDAL1(.MVT,ASH(.21,"I"),FLD)
 S PARAM("ADMIFN")=MVT(.14,"I")
 S TRMAS=$S(+TYPE=46:45,+TYPE=41:14,1:0)
 S PARAM("TYPE")=$$GETAMT^DGPMDAL3(TRMAS)
 S PARAM("ASHSEQ")=2
 S %=$$ADDTRA^DGPMAPI2(.TRA,.PARAM)
 Q %
 ;
ADDDIS(RETURN,PARAM,DFN,TYPE) ;
 N MVT,ADMIFN,DGDT,PM
 D GETMVT^DGPMDAL1(.MVT,+PARAM("ADMIFN"),".01;.03;.06;.16")
 S ADMIFN=+PARAM("ADMIFN")
 S DGDT=+PARAM("DATE")
 S PM(.01)=DGDT ; discharge date
 D ADDMVMTX^DGPMDAL1(.RETURN,.PM)
 S IFN=+RETURN
 ;D SETDEVT^DGPMDAL1(IFN,ADMIFN,"P")
 K PM
 S PM(.14)=ADMIFN
 S PM(.02)=3  ; transaction
 S PM(.04)=+PARAM("TYPE")  ; type of movement
 S PM(.03)=DFN  ; patient
 S PM(.05)=$G(PARAM("FCTY"))  ; transfer facility
 S:+$G(TYPE)=46!(+$G(TYPE)=41) PM(.22)=1  ; ASIH sequence
 S PM(100)=DUZ,PM(101)=$$NOW^XLFDT()
 S PM(102)=DUZ,PM(103)=$$NOW^XLFDT()
 D UPDMVT^DGPMDAL1(.RETURN,.PM,IFN)
 S IFNP=MVT(.16,"I")
 S PTF(70)=DGDT
 D GETMVT^DGPMDAL1(.MVT,IFN,".01;.18")
 D GETMASMT^DGPMDAL2(.MAS,MVT(.18,"I"),".01;.08")
 S PTF(72)=MAS(.08,"I")
 D UPDPTF^DGPMDAL1(.RPTF,.PTF,IFNP)
 S PAT(401.3)="@"
 S PAT(401.4)="@"
 D RESET^DGPMDDCN
 S %=$$UPDPAT^DGPMAPI7(,.PAT,+DFN)
 Q IFN
 ;
CHKUPD(RETURN,PARAM,DMFN,OLD,NEW) ; Check transfer parameters
 N %,TXT,MTYPE,WARD,DATE,NMVT K RETURN S RETURN=1
 ; transfer date
 D GETMVT^DGPMDAL1(.OLD,+$G(DMFN))
 I OLD=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"DCHNFND") Q 0
 I $G(PARAM("DATE"))'="",+$G(OLD(.01,"I"))'=+$G(PARAM("DATE")) D  Q:'RETURN 0
 . D GETAPRD^DGPMDAL1(.NMVT,+OLD(.03,"I"),+PARAM("DATE"))
 . I $G(PARAM("DATE"))=""!(+$G(PARAM("DATE"))<1800000) D  Q
 . . S TXT(1)="PARAM('DATE')",RETURN=0 D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 . ;not before last movement
 . I $D(NMVT),NMVT("ID")'=+DMFN S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"DCHNBLM") Q
 . I $$TIMEUSD^DGPMDAL2(+OLD(.03,"I"),+PARAM("DATE")) D  Q
 . . S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TIMEUSD")
 . S NEW("DATE")=+PARAM("DATE")
 S DATE=$S($D(NEW("DATE")):+NEW("DATE"),1:+OLD(.01,"I"))
 ; type of movement
 I $G(PARAM("TYPE"))'="",+$G(OLD(.04,"I"))'=+$G(PARAM("TYPE")) D  Q:'RETURN 0
 . S %=$$CHKDTYP(.RETURN,+$G(PARAM("TYPE")),+OLD(.03,"I"),DATE) Q:'%
 . S NEW("TYPE")=+PARAM("TYPE")
 S TYPE=$S($D(NEW("TYPE")):+NEW("TYPE"),1:+OLD(.04,"I"))
 S RETURN=($D(NEW)>0)
 Q 1
 ;
UPDDSCH(RETURN,PARAM,DMFN) ; Update discharge
 N OLD,NEW
 K RETURN S RETURN=0
 S %=$$CHKUPD(.RETURN,.PARAM,.DMFN,.OLD,.NEW)
 Q:%=0 0
 I RETURN=0 S:'$D(RETURN(0)) RETURN=1 Q $S('$D(RETURN(0)):1,1:0)
 D SETDEVT^DGPMDAL1(+DMFN,"P")
 S:$D(NEW("DATE")) DIS(.01)=+NEW("DATE")
 S:$D(NEW("TYPE")) DIS(.04)=+NEW("TYPE")
 S:$D(NEW("FCTY")) DIS(.05)=+NEW("FCTY")
 D UPDMVT^DGPMDAL1(,.DIS,+DMFN)
 D SETDEVT^DGPMDAL1(+DMFN,"A")
 D MVTEVT^DGPMAPI7(+PARAM("PATIENT"),3,+DMFN)
 S RETURN=1
 Q 1
 ;
CANDEL(RETURN,DMFN) ; Can delete discharge?
 N LMVT,PMVT,ASHADM,DCH,FLDS,ADM
 K RETURN S RETURN=0,FLDS=".01;.02;.03;.04;.14;.15;.16;.17;.18;.21"
 D GETMVT^DGPMDAL1(.DCH,DMFN,FLDS)
 I DCH=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"DCHNFND") Q 0
 S %=$$GETLASTM^DGPMAPI8(.LMVT,+DCH(.03,"I"))
 I DCH(.18,"I")=42 D ERRX^DGPMAPIE(.RETURN,"DCHCDWAH") Q 0
 I $G(LMVT("MFN"))'=+DMFN D ERRX^DGPMAPIE(.RETURN,$S(LMVT("TYPE")<4:"DCHCDOLA",1:"DCHDODLM")) Q 0
 D GETMVT^DGPMDAL1(.ADM,DCH(.14,"I"),FLDS)
 I ADM(.21,"I"),("^41^46^"[("^"_+DCH(.18,"I")_"^")) D ERRX^DGPMAPIE(.RETURN,"DCHDTCNH") Q 0
 I DCH(.18,"I")=47,("^13^44^"[("^"_ADM(.18,"I")_"^")) D  Q:'RETURN 0
 . D GETPMVT^DGPMDAL3(.PMVT,DCH(.03,"I"),DCH(.14,"I"),,DMFN,FLDS)
 . D GETMVT^DGPMDAL1(.ASHADM,$G(PMVT(.15,"I")),FLDS)
 . I $D(ASHADM),$G(ASHADM(.17,"I")) Q
 . S RETURN=1
 S RETURN=1
 Q 1
 ;
DELDSCH(RETURN,DMFN) ; Delete discharge
 N %,DIS,DA
 K RETURN S RETURN=0,DA=+DMFN
 S %=$$CANDEL^DGPMAPI3(.RETURN,+DMFN) Q:'RETURN 0
 D INITEVT^DGPMDAL1
 D GETMVT^DGPMDAL1(.DIS,+DMFN,".03;.14;.16;.17;.21")
 D SETAEVT^DGPMDAL1(DIS(.14,"I"),,"P")
 D SETDEVT^DGPMDAL1(+DMFN,"P")
 D DELMVT^DGPMDAL1(+DMFN)
 D SETAEVT^DGPMDAL1(DIS(.14,"I"),,"A")
 D SETDEVT^DGPMDAL1(+DMFN,"A")
 S %=$$UPDPAT^DGPMAPI7(,.PARAM,+DIS(.03,"I"))
 D MVTEVT^DGPMAPI7(DIS(.03,"I"),3,+DMFN)
 S RETURN=1
 Q 1
 ;
