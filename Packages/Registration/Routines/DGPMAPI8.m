DGPMAPI8 ;RGI/VSL - PATIENT MOVEMENT API; 2/11/2013
 ;;5.3;Registration;**260005**;
GETLASTM(RETURN,DFN,DGDT) ; Get last patient movement
 N %,DGDT1,LMVT,MVT,FLDS,ADM,RPM,PAT
 K RETURN S RETURN=0
 I '$D(DGDT) D NOW^%DTC S DGDT=%
 S DGDT1=9999999.999999-DGDT
 D GETLASTM^DGPMDAL1(.LMVT,+DFN,DGDT1)
 Q:LMVT(1)'>0 1
 D GETPAT^DGPMDAL2(.PAT,+DFN,"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 D GETMVT^DGPMDAL1(.MVT,+LMVT(1),".04")
 S RETURN("MTYPE")=MVT(.04,"I")_U_MVT(.04,"E")
 S RETURN("MFN")=LMVT(1)
 S RETURN("TYPE")=LMVT(2)
 S RETURN("DATE")=LMVT(3)
 S RETURN("MASTYPE")=LMVT(4)
 S RETURN("WARD")=LMVT(5)
 S RETURN("ROOMBED")=LMVT(6)
 S RETURN("PRYMPHY")=LMVT(7)
 S RETURN("FTSPEC")=LMVT(8)
 S RETURN("ADMIFN")=LMVT(13)
 S RETURN("ADMDT")=LMVT(13,1)
 S RETURN("DISIFN")=LMVT(17)
 S RETURN("ATNDPHY")=LMVT(18)
 S RETURN("FDEXC")=LMVT(19,1)
 S RETURN=1
 Q 1
 ;
GETPAT(RETURN,DFN) ; Get patient
 K RETURN N IND,NAME,FLDS,NAMES,PAT
 S FLDS=".01;.02;.03;.05;.06;.08;.14;.351;.361;.3611"
 S NAMES="NAME;SEX;DOB;MSTAT;RACE;RELIG;MEANST;DTHDT;ELIG;ESTAT"
 D GETPAT^DGPMDAL2(.PAT,+DFN,FLDS)
 I PAT=0 M RETURN=PAT Q 0
 D BUILD(.RETURN,.PAT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETADM(RETURN,AFN) ; Get admission
 N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT,DIAG,PTF
 S FLDS=".01;.02;.03;.04;.06;.07;.1;.11;.12;.17;.18;41;.16"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOMBED;SHDIAG;ADMSCC;ADMREG;DISCH;MASTYPE;FDEXC;PTF"
 D GETMVT^DGPMDAL1(.MVT,AFN,FLDS)
 I MVT=0 M RETURN=MVT Q 0
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 D GETPTF^DGPMDAL1(.PTF,+RETURN("PTF"),"20;20.1")
 S RETURN("ADMSRC")=PTF(20,"I")_U_PTF(20,"E")
 S RETURN("ELIGIB")=PTF(20.1,"I")_U_PTF(20.1,"E")
 D GETPAT^DGPMDAL2(.PAT,+RETURN("PATIENT"),"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 S FLDS=".08;.09;.19"
 S NAMES="PRYMPHY;FTSPEC;ATNDPHY"
 S RPHY=$$GETRPHY^DGPMDAL1(AFN)
 D GETMVT^DGPMDAL1(.RPHYMVT,RPHY,FLDS)
 D GETDIAG^DGPMDAL2(.DIAG,RPHY)
 M RETURN("DIAG")=DIAG(99)
 D BUILD(.RETURN,.RPHYMVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETTRA(RETURN,AFN) ; Get transfer
 K RETURN N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT,DIAG
 S FLDS=".01;.02;.03;.04;.06;.07;.13;.14;.18;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOMBED;RABSDT;ADMIFN;MASTYPE;"
 D GETMVT^DGPMDAL1(.MVT,AFN,FLDS)
 I MVT=0 M RETURN=MVT Q 0
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 D GETPAT^DGPMDAL2(.PAT,+RETURN("PATIENT"),"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 S FLDS=".08;.09;.19"
 S NAMES="PRYMPHY;FTSPEC;ATNDPHY"
 S RPHY=$$GETRPHY^DGPMDAL1(AFN)
 I $G(RPHY)>0 D
 . D GETMVT^DGPMDAL1(.RPHYMVT,RPHY,FLDS)
 . D GETDIAG^DGPMDAL2(.DIAG,RPHY)
 . M RETURN("DIAG")=DIAG(99)
 . D BUILD(.RETURN,.RPHYMVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMVT(RETURN,AFN) ; Get movement
 K RETURN N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT
 S FLDS=".01;.02;.03;.04;.05;.06;.07;.08;.09;.1;.14;.17;.18"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;FCTY;WARD;ROOMBED;PRYMPHY;FTSPEC;SHDIAG;ADMIFN;DISCH;MASTYPE"
 D GETMVT^DGPMDAL1(.MVT,+AFN,FLDS)
 I MVT=0 M RETURN=MVT Q 0
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMVTT(RETURN,IFN) ; Get movement type
 K RETURN N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT
 S FLDS=".01;.02;.03;.04;.05;"
 S NAMES="NAME;TTYPE;MAS;STAT;ASKSPEC"
 D GETMVTT^DGPMDAL2(.MVT,+IFN,FLDS)
 I MVT=0 M RETURN=MVT Q 0
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMASMT(RETURN,IFN) ; Get MAS movement type
 K RETURN N IND,NAME,FLDS,NAMES,MVT
 S FLDS=".01;.02;.05;.06;50.01;50.02;50.03;"
 S NAMES="NAME;TTYPE;ASKSPEC;ASKFTY;ABS;CFADM;ASIH"
 D GETMASMT^DGPMDAL2(.MVT,+IFN,FLDS)
 I MVT=0 M RETURN=MVT Q 0
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
BUILD(RETURN,REC,FLDS,NAMES) ; 
 N IND
 F IND=0:0 S IND=$O(REC(IND)) Q:IND=""  D
 . S NAME=$$FLDNAME^SDMUTL(FLDS,NAMES,IND)
 . S RETURN(NAME)=REC(IND,"I")_U_REC(IND,"E")
 Q
ISDOM(RETURN,DFN,DGDT) ; Is patient on dom?
 K RETURN N VAINDT,VADMVT,DGDOM,DGDOM1
 S:$D(DGDT) VAINDT=DGDT
 S DFN=+DFN,DGDT=+DGDT
 D DOM^DGMTR
 S RETURN=$G(DGDOM)
 Q 1
CHKWARD(RETURN,WARD,DATE) ; Check ward
 N TMP,TXT K RETURN S RETURN=0
 I $G(WARD)="" S TXT(1)="PARAM('WARD')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETWARD^DGPMDAL2(.TMP,+WARD,".01;400;200*")
 I TMP=0 D ERRX^DGPMAPIE(.RETURN,"WRDNFND") Q 0
 I TMP=1,TMP(400,"I")="" D ERRX^DGPMAPIE(.RETURN,"WRDINVGL",.TXT) Q 0
 I TMP=1,$D(TMP("OOS")),'$$ISWRDACT^DGPMAPI7(.TMP,+DATE) D ERRX^DGPMAPIE(.RETURN,"WRDINACT") Q 0
 S RETURN=1
 Q 1
 ;
CHKBED(RETURN,BED,WARD,DFN,DATE) ; Check bed
 N TMP,TXT K RETURN S RETURN=0
 D GETWARD^DGPMDAL2(.TMP,+$G(WARD),".01;")
 N ERR,WN S WN=$G(TMP(.01,"E")) K TMP
 D GETBED^DGPMDAL2(.TMP,+$G(BED),".01;.2;100*;200*")
 I TMP=0 S ERR=1 D ERRX^DGPMAPIE(.RETURN,"BEDNFND",.TXT) Q 0
 N I,WF S I="",WF=0
 F  S I=$O(TMP("W",I)) Q:I=""  S:TMP("W",I,.01,"I")=+$G(WARD) WF=1
 I 'WF S ERR=1,TXT(1)=WN,TXT(2)=TMP(.01,"E") D ERRX^DGPMAPIE(.RETURN,"WRDCNASB",.TXT) Q 0
 I $D(TMP("OOS")),'$$ISBEDACT^DGPMAPI7(.TMP,+$G(DATE)) S ERR=1 D ERRX^DGPMAPIE(.RETURN,"BEDINACT",.TXT) Q 0
 I $$ISBEDOCC^DGPMAPI7(+$G(BED),+$G(DFN)) S ERR=1 D ERRX^DGPMAPIE(.RETURN,"BEDOCC") Q 0
 S RETURN=1
 Q 1
 ;
CHKTYPE(RETURN,TYPE,DFN,DATE,MVT) ; Check type
 N TXT,ADTYP,ERR K RETURN S RETURN=0
 I $G(TYPE)="" S TXT(1)="PARAM('TYPE')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETMVTT^DGPMDAL2(.TMP,+TYPE)
 I TMP=0 D ERRX^DGPMAPIE(.RETURN,"MVTTNFND") Q 0
 S RETURN=1
 Q 1
 ;
CHKPAT(RETURN,DFN) ; Check patient
 N TMP,TXT K RETURN S RETURN=0
 I $G(DFN)="" S TXT(1)="PARAM('PATIENT')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETPAT^DGPMDAL2(.TMP,+DFN)
 I TMP=0 D ERRX^DGPMAPIE(.RETURN,"PATNFND") Q 0
 S RETURN=1
 Q 1
 ;
CHKAREG(RETURN,ADMREG) ; Check admitting regulation
 N TMP,TXT K RETURN S RETURN=0
 I $G(ADMREG)="" S TXT(1)="PARAM('ADMREG')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETAREG^DGPMDAL2(.TMP,+ADMREG)
 I TMP=0 D ERRX^DGPMAPIE(.RETURN,"AREGNFND") Q 0
 I $G(TMP(4,"I"))=1 S TXT(1)=TMP(.01,"E") D ERRX^DGPMAPIE(.RETURN,"AREGINAC",.TXT) Q 0
 S RETURN=1
 Q 1
 ;
CHKASH(RETURN,PARAM,DFN,MAS) ;
 N %,WRD
 S RETURN=0
 S %=$$CHKADM^DGPMAPI1(.RETURN,.PARAM,1) Q:'RETURN&($P($G(RETURN(0)),U)'="WRDCNASB") 0
 K RETURN
 D GETWARD^DGPMDAL2(.WRD,+PARAM("WARD"),".01;.03;.17")
 I TMP=1,"^NH^D^"[(U_WRD(.03,"I")_U) S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"ASHWINVS") Q 0
 S RETURN=1
 Q 1
 ;
CHKFCTY(RETURN,ADMREG) ; Check asih facility
 N TMP,TXT K RETURN S RETURN=0
 I $G(ADMREG)="" S TXT(1)="PARAM('FCTY')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETFCTY^DGPMDAL2(.TMP,+ADMREG)
 I TMP=0 D ERRX^DGPMAPIE(.RETURN,"TFCNFND") Q 0
 I $G(TMP(101,"I"))=1 S TXT(1)=TMP(.01,"E") D ERRX^DGPMAPIE(.RETURN,"TFCINAC",.TXT) Q 0
 S RETURN=1
 Q 1
 ;
CHKTTYP(RETURN,TYPE,DFN,DATE) ; Check transfer type
 N %,TMP,TXT,ADTYP,ERR K RETURN S RETURN=0
 S %=$$CHKTYPE^DGPMAPI8(.RETURN,.TYPE,+DFN,+DATE,.TMP) Q:'RETURN 0
 S RETURN=0
 S %=$$LSTTRTYP^DGPMAPI7(.ADTYP,+TYPE,,,+DFN,+DATE)
 S TXT(1)=TMP(.01,"E")
 I $G(TMP(.04,"I"))'=1 D ERRX^DGPMAPIE(.RETURN,"MVTTINAC",.TXT) Q 0
 I +$G(ADTYP(0))=0 D ERRX^DGPMAPIE(.RETURN,"TRAINVAT",.TXT) Q 0
 S RETURN=1
 Q 1
 ;
CHKREG(RETURN,PARAM,DFN,MAS) ;
 N %
 ; ward
 S %=$$CHKWARD^DGPMAPI8(.RETURN,$G(PARAM("WARD")),+PARAM("DATE")) Q:'RETURN 0
 ; roombed
 I $G(PARAM("ROOMBED"))'="" D  Q:'RETURN 0
 . S %=$$CHKBED^DGPMAPI8(.RETURN,+PARAM("ROOMBED"),+PARAM("WARD"),DFN,+PARAM("DATE"))
 ; related physical movement
 I MAS(.05,"I"),$D(PARAM("FTSPEC"))!$D(PARAM("ATNDPHY")) D  Q:'% 0
 . S %=$$CHKRPM^DGPMAPI6(.RETURN,.PARAM)
 Q 1
 ;
