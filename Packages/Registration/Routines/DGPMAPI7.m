DGPMAPI7 ;RGI/VSL - PATIENT MOVEMENT API; 12/14/2012
 ;;5.3;Registration;**260003**;
ISBEDOCC(BED,DFN) ; Is bed occupied?
 N BEDS,TMP,OCC,I
 S I=0,OCC=0
 D LSTOCBED^DGPMDAL2(.TMP)
 M BEDS=TMP("DILIST","ID")
 F  S I=$O(BEDS(I)) Q:'I!OCC  D
 . I BEDS(I,.07)=BED&(BEDS(I,.03)'=DFN) S OCC=1 Q
 Q OCC
 ;
ISFTSACT(FTS,DGDT) ; Is facility treating specialty active on date?
 I $D(FTS)=1 D GETFTS^DGPMDAL2(.FTS,FTS,".01;100*")
 N I,ACT,OUT
 S OUT=0,I=0,ACT=1,D0=$S($D(DGDT):DGDT,1:DT),D0=$P(D0,".")
 F  S I=$O(FTS("E",I)) Q:'I!(OUT)  D
 . S D1=FTS("E",I,.01,"I")
 . I D0<D1 S OUT=1 Q
 . S ACT=FTS("E",I,.02,"I")
 Q ACT
 ;
ISBEDACT(BED,DGDT) ; Is bed active on date?
 I $D(BED)=1 D GETBED^DGPMDAL2(.BED,BED,".01;.2;100*;200*")
 N I,ACT,D0,D1,D2
 S I=0,ACT=1,D0=$S($D(DGDT):DGDT,1:DT),D0=$P(D0,".")
 F  S I=$O(BED("OOS",I)) Q:'I!('ACT)  D
 . S D1=BED("OOS",I,.01,"I")
 . S D2=BED("OOS",I,.04,"I")
 . I $S(D1<D0:1,D1=D0:1,1:0)&$S(D2="":1,D0<D2:1,1:0) S ACT=0 Q
 Q ACT
 ;
ISWRDACT(WARD,DGDT) ; Is ward active on date?
 I $D(WARD)=1 D GETWARD^DGPMDAL2(.WARD,WARD,".01;400;200*")
 N I,ACT
 S I=0,ACT=1,D0=$S($D(DGDT):DGDT,1:DT),D0=$P(D0,".")
 F  S I=$O(WARD("OOS",I)) Q:'I!('ACT)  D
 . I 'WARD("OOS",I,.06,"I") Q
 . S D1=WARD("OOS",I,.01,"I")
 . S D2=WARD("OOS",I,.04,"I")
 . I $S(D1<D0:1,D1=D0:1,1:0)&$S(D2="":1,D0<D2:1,1:0) S ACT=0 Q
 Q ACT
 ;
LSTPROV(RETURN,SEARCH,START,NUMBER,DGDT) ; Return active providers
 N FLDS,NAMES,PROV
 S FLDS=".01;1;8;"
 S NAMES="NAME;INITIAL;TITLE;",SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTPROV^DGPMDAL2(.PROV,.SEARCH,.START,.NUMBER,FLDS,.DGDT)
 D BLDLST(.RETURN,.PROV,FLDS,NAMES)
 Q 1
 ;
LSTADREG(RETURN,SEARCH,START,NUMBER) ; Return admitting regulations
 N FLDS,NAMES,ADREG
 S FLDS=".01;2;"
 S NAMES="NAME;CFR;",SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTADREG^DGPMDAL2(.ADREG,.SEARCH,.START,.NUMBER,FLDS)
 D BLDLST(.RETURN,.ADREG,FLDS,NAMES)
 Q 1
 ;
LSTFTS(RETURN,SEARCH,START,NUMBER,DGDT) ; Return facility treating specialties
 N FLDS,NAMES,ADREG
 S FLDS=".01;1;"
 S NAMES="NAME;SPEC;",SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTFTS^DGPMDAL2(.ADREG,.SEARCH,.START,.NUMBER,FLDS,DGDT)
 D BLDLST(.RETURN,.ADREG,FLDS,NAMES)
 Q 1
 ;
LSTADSRC(RETURN,SEARCH,START,NUMBER) ; Return source of admission
 N FLDS,NAMES,ADREG
 S FLDS=".01;2;11"
 S NAMES="CODE;NAME;PLACE;",SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTADSRC^DGPMDAL2(.ADREG,.SEARCH,.START,.NUMBER,FLDS)
 D BLDLST(.RETURN,.ADREG,FLDS,NAMES)
 Q 1
 ;
LSTADTYP(RETURN,SEARCH,START,NUMBER) ; Return movement types
 N FLDS,NAMES,TYPES
 S FLDS=".01;.02;.04"
 S NAMES="NAME;TYPE;STATUS",SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTMVTT^DGPMDAL2(.TYPES,.SEARCH,.START,.NUMBER,FLDS,1)
 D BLDLST(.RETURN,.TYPES,FLDS,NAMES)
 Q 1
 ;
LSTWARD(RETURN,SEARCH,START,NUMBER) ; Return wards
 N FLDS,NAMES,WRD
 S FLDS=".01;.017IE;.03IE;"
 S NAMES="NAME;SPEC;SERV"
 S SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTWARD^DGPMDAL2(.WRD,.SEARCH,.START,.NUMBER,FLDS)
 D BLDLST(.RETURN,.WRD,FLDS,NAMES)
 Q 1
 ;
LSTWBED(RETURN,SEARCH,START,NUMBER,WARD,DFN) ; Return beds
 N FLDS,NAMES,BED,OCB,TMP,TBED,CNT,J,I,OCUP
 S FLDS=".01;.02;.2;"
 S NAMES="NAME;DESC;OOS"
 S SEARCH=$$UP^XLFSTR($G(SEARCH))
 D LSTWBED^DGPMDAL2(.BED,.SEARCH,.START,.NUMBER,FLDS,.WARD)
 D BLDLST(.TBED,.BED,FLDS,NAMES)
 D LSTOCBED^DGPMDAL2(.TMP)
 M OCB=TMP("DILIST","ID")
 S CNT=0
 F J=0:0 S J=$O(TBED(J)) Q:J=""  D
 . S OCUP=0
 . F I=0:0 S I=$O(OCB(I)) Q:I=""  D
 . . I TBED(J,"ID")=OCB(I,.07),OCB(I,.03)'=$G(DFN) S OCUP=1
 . I 'OCUP S CNT=CNT+1 M RETURN(CNT)=TBED(J)
 S RETURN(0)=CNT
 Q 1
 ;
LSTPATS(RETURN,SEARCH,START,NUMBER) ; Get patients by name
 N RET,DL,IN,DG
 S:'$D(START) START="" S:'$D(SEARCH) SEARCH=""
 S:'$G(NUMBER) NUMBER=""
 S RETURN=0
 D LSTPATS^DGPMDAL2(.RET,$$UP^XLFSTR(SEARCH),.START,NUMBER)
 S RETURN(0)=RET("DILIST",0)
 S DL="DILIST"
 F IN=1:1:$P(RETURN(0),U,1) D
 . D SENS^DGSEC4(.DG,RET(DL,2,IN),DUZ)
 . S RETURN(IN)=""
 . S RETURN(IN,"ID")=RET(DL,2,IN)
 . S RETURN(IN,"NAME")=RET(DL,"ID",IN,".01")
 . S RETURN(IN,"BIRTH")=$S(DG(1)=2:"*SENSITIVE*",1:RET(DL,"ID",IN,".03"))
 . S RETURN(IN,"SSN")=$S(DG(1)=2:"*SENSITIVE*",1:RET(DL,"ID",IN,".09"))
 . S RETURN(IN,"TYPE")=RET(DL,"ID",IN,"391")
 . S RETURN(IN,"VETERAN")=RET(DL,"ID",IN,"1901")
 S RETURN=1
 Q 1
 ;
LSTPADMS(RETURN,DFN) ; Returns patient admissions.
 N FLDS,NAMES,ADM
 S FLDS=".01IE;.02I;.03IE;.04IE;.06IE;.07IE;.08IE;.09IE;.1;.17I;.18IE"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;BED;PRYMPHY;FTSPEC;SHDIAG;DISCH;MASTYPE"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,1,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 Q 1
 ;
LSTPTRAN(RETURN,DFN) ; Returns patient transfers.
 N FLDS,NAMES,ADM
 S FLDS=".01I;.02I;.03IE;.04IE;.06IE;.07IE;.08IE;.09IE;.1;.17I;.18IE"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOM;PRYMPHY;FTSPEC;SHDIAG;DISCH;MASTYPE"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,2,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 Q 1
 ;
LSTPDSCH(RETURN,DFN) ; Returns patient discharges.
 N FLDS,NAMES,ADM
 S FLDS=".01I;.02I;.03IE;.04IE;.14I;.18IE"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;ADMISS;MASTYPE"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,3,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 Q 1
 ;
LSTPLCI(RETURN,DFN) ; Returns patient lodger check-ins.
 N FLDS,NAMES,ADM
 S FLDS=".01I;.02I;.03IE;.04IE;.06IE;.07IE;.17I;.18IE;30.01IE;30.02;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOM;DISCH;MASTYPE;REASON;COMM"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,4,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 Q 1
 ;
LSTPLCO(RETURN,DFN) ; Returns patient lodger check-outs.
 N FLDS,NAMES,ADM
 S FLDS=".01I;.02I;.03IE;.04IE;.14I;.18IE;30.03IE;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;CHECKIN;MASTYPE;DISPOS;"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,5,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 Q 1
 ;
LSTPTST(RETURN,DFN) ; Returns patient treating specialty transfers.
 N FLDS,NAMES,ADM
 S FLDS=".01I;.02I;.03IE;.04IE;.08IE;.09IE;.14I;.18IE;.19IE;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;PRYMPHY;FTSPEC;ADMISS;MASTYPE;ATNDPHY;"
 D LSTPMVT^DGPMDAL2(.ADM,DFN,6,FLDS)
 D BLDLST(.RETURN,.ADM,FLDS,NAMES)
 D LSTDIAG^DGPMDAL2(.RETURN)
 F I=0:0 S I=$O(RETURN(I)) Q:I=""  M RETURN(I,"DIAG")=RETURN(I,99) K RETURN(I,99)
 Q 1
 ;
BLDLST(RETURN,LST,FLDS,NAMES) ; Build list
 N IND,FLD,NAME,D1,D2,ID
 S D1="DILIST",D2="ID"
 S RETURN(0)=$G(LST("DILIST",0))
 F IND=0:0 S IND=$O(LST(D1,2,IND)) Q:IND=""  D
 . S ID=LST("DILIST",2,IND)
 . S RETURN(IND,"ID")=ID
 . F FLD=0:0 S FLD=$O(LST(D1,D2,IND,FLD)) Q:FLD=""  D
 . . S NAME=$$FLDNAME^SDMUTL(FLDS,NAMES,FLD)
 . . I $D(LST(D1,D2,IND,FLD,"I"))!$D(LST(D1,D2,IND,FLD,"E")) D
 . . . S RETURN(IND,NAME)=$G(LST(D1,D2,IND,FLD,"I"))_U_$G(LST(D1,D2,IND,FLD,"E")) Q
 . . E  S RETURN(IND,NAME)=$G(LST(D1,D2,IND,FLD))
 S RETURN=1
 Q
 ;
