DGPMVD ;RGI/VSL - DISCHARGE PATIENT; 1/10/2013
 ;;5.3;Registration;**260003**;Aug 13, 1993
 ;
DISCH ;
 N DFN
 S DFN=$$SELPAT^DGPMVN("LSTTPATS^DGPMVD",4070000.101) G:DFN=-1 DISCH Q:'DFN
 D MEANST^DGPMVN(DFN)
 D PATSTAT^DGPMVN(DFN,1)
 S %=$$CONTINUE^DGPMVN()
 G:%="Q"!(%="") DISCH
DISCH1 ;
 N RE,ERR,DIS,LMVT,DATE
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S DATE=$$SELDSCH(.LMVT,.NEW,4070000.102)
 I DATE="@" D DELDSCH(LMVT("MFN")) G DISCH
 G:DATE'>0 DISCH
 I NEW D NEWDSCH(.RE,DATE,DFN,+$G(LMVT("ADMIFN"))) I 1
 E  D UPDDSCH(.RE,LMVT("MFN"),DATE)
 I RE=0 D
 . I $P(RE(0),U)="FILELOCK" D BLD^DIALOG(4070000.083,,,"ERR") I 1
 . E  S ERR(1)=$P(RE(0),U,2)
 . D EN^DDIOL(.ERR)
 G DISCH
 Q
 ;
SELDSCH(LMVT,NEW,PROMPT) ;
 N I,LN,LNO,TMP,DDT,X,Y,DIR S NEW=1
SELDSCH1 ;
 I LMVT("TYPE")=3 S NEW=0
 S DDT=$S(NEW:$$EZBLD^DIALOG(37007),1:$P(LMVT("DATE"),U,2))
 S DIR(0)="FOr^"
 S DIR("A")=$$EZBLD^DIALOG(PROMPT),DIR("B")=DDT
 D ^DIR
 I Y="" S Y=DDT
 Q:X="@" X
 S X=Y,%DT="SEXT",%DT(0)="-NOW" D ^%DT
 I 'NEW,Y=+LMVT("DATE") Q Y
 Q:Y>0 Y
 Q:NEW&&(X=""!(X="^")) Y
 G SELDSCH1
 Q 0
 ;
UPDDSCH(RETURN,DMFN,DATE) ;
 N ODIS,DIS,DELETED
 S RETURN=1
 S %=$$GETDIS^DGPMAPI8(.ODIS,DMFN)
UPD ;
 S DIS("DATE")=+DATE
 S DIS("ADMIFN")=+ODIS("ADMIFN")
 S DIS("PATIENT")=+ODIS("PATIENT")
 D SELALL(.DIS,.ODIS,+ODIS("PATIENT"),DMFN)
 S %=$$UPDDSCH^DGPMAPI3(.RETURN,.DIS,DMFN)
 Q
 ;
DELDSCH(DMFN) ; Delete discharge
 N %,TXT,PARAM,DEFAULT,RETURN,ERR
 S TXT(1)="ADMISSION"
 S PARAM("PROMPT")=4070000.084,PARAM("HELPT")=$$EZBLD^DIALOG(4070000.085,.TXT)
 S DEFAULT=2
 S %=$$CANDEL^DGPMAPI3(.ERR,DMFN)
 I ERR=0 D EN^DDIOL($P(ERR(0),U,2)) Q 0
 S %=$$YN^DGPMUTL1(.PARAM,.DEFAULT)
 Q:%'=1 0
 S %=$$DELDSCH^DGPMAPI3(.RETURN,DMFN)
 I RETURN=0 D EN^DDIOL($P(RETURN(0),U,2)) Q 0
 Q 1
 ;
NEWDSCH(RETURN,DMFN,DFN,LMVT) ;
 N %,DIS,RE,DIAG,TXT
 S TXT(1)=$$FMTE^XLFDT(DMFN)
 S %=$$ASK^DGPMVN(4070000.094,4070000.002,1,.TXT)
 I %'=1 G DISCH1
 S DIS("ADMIFN")=LMVT
 S DIS("DATE")=DMFN
 S DIS("PATIENT")=DFN
 D SELALL(.DIS,,DFN)
 S %=$$DISCH^DGPMAPI3(.RETURN,.DIS)
 Q
 ;
SELALL(DIS,ODIS,DFN,DMFN) ;
 N RE,DIAG,TYPE,TT,MT
 S DIS("TYPE")=$$SELTYPE($G(ODIS("TYPE")),DFN,$G(ODIS("DATE"),DIS("DATE")),.DMFN)
 D GETMVTT^DGPMAPI8(.TT,DIS("TYPE"))
 S TYPE=TT("MAS")
 S %=$$GETMASMT^DGPMAPI8(.MT,TYPE)
 I MT("ASKFTY") S DIS("FCTY")=$$SELFCTY^DGPMVT(4070000.103,$G(ODIS("FCTY")))
 I +TYPE=41 D
 . S DIS("WARD")=$$SELWARD^DGPMVA($G(ODIS("WARD")))
 . S DIS("ROOMBED")=$$SELBED^DGPMVA($G(DIS("WARD")),$G(ODIS("ROOMBED")))
 . D FTS^DGPMVT(.DIS,.ODIS,DFN)
 . S DIS("SERILL")=$$SERILL^DGPMVA($G(OTRA("SERILL"),"S^SERIOUSLY ILL"))
 Q
SELTYPE(DEFAULT,DFN,DGDT,DMFN) ; Select transfer types
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.1)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTDTYP^DGPMVD",PAR("FLDS")="ID;NAME;TYPE;STATUS",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.019,PAR("HELP2")=4070000.02,PAR("HELP3")=4070000.021
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
LSTTPATS(RETURN,SEARCH,START,NUMBER) ; Lists patients
 S %=$$LSTTPATS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTDTYP(RETURN,SEARCH,START,NUMBER) ; Lists discharge types
 S %=$$LSTDTYP^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,DFN,.DGDT,.DMFN)
 Q
