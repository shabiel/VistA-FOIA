DGPMAPI2 ;RGI/VSL - TRANSFER PATIENT API; 2/20/2013
 ;;5.3;Registration;**260005**;
CHKDT(RETURN,PARAM) ; Check transfer date
 ; transfer date
 I $G(PARAM("DATE"))=""!(+$G(PARAM("DATE"))<1800000) D  Q 0
 . S TXT(1)="PARAM('DATE')",RETURN=0 D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 I $G(PARAM("ADMIFN"))="" S RETURN=0,TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETMVT^DGPMDAL1(.ADM,+$G(PARAM("ADMIFN")))
 I ADM=0 S TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 S PARAM("PATIENT")=ADM(.03,"I")
 I $$TIMEUSD^DGPMDAL2(+PARAM("PATIENT"),+PARAM("DATE")) D  Q 0
 . S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TIMEUSD")
 ;not before admission
 I +PARAM("DATE")<+ADM(.01,"I") S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANBADM") Q 0
 I ADM(.17,"I")>0 D
 . D GETMVT^DGPMDAL1(.DSH,+ADM(.17,"I"),".01")
 . I +PARAM("DATE")>+DSH(.01,"I") S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANADIS") Q
 Q 1
CHKTRA(RETURN,PARAM,TYPE,MAS,LMVT) ; Check transfer parameters
 N %,TXT,TT,ADM,WARD K RETURN S RETURN=1
 ; transfer date
 S %=$$GETLASTM^DGPMAPI8(.LMVT,+ADM(.03,"I"),+PARAM("DATE"))
 ; type of movement
 S %=$$CHKTTYP^DGPMAPI8(.RETURN,$G(PARAM("TYPE")),+ADM(.03,"I"),+PARAM("DATE")) Q:'RETURN 0
 D GETMVTT^DGPMDAL2(.TT,+PARAM("TYPE"))
 S TYPE=TT(.03,"I")
 D GETMASMT^DGPMDAL2(.MAS,TYPE)
 I "^1^2^3^23^25^26^"[(U_TYPE_U) S RETURN=1 Q 1
 I 1 D  Q:'RETURN 0
 . I TYPE=13 S %=$$CHKASH^DGPMAPI8(.RETURN,.PARAM) Q
 . I TYPE=43 S %=$$CHKFCTY^DGPMAPI8(.RETURN,$G(PARAM("FCTY"))) Q
 . S %=$$CHKREG^DGPMAPI8(.RETURN,.PARAM,+ADM(.03,"I"),.MAS)
 S RETURN=1
 Q 1
TRANSF1(RETURN,PARAM,TYPE,MAS,LMVT) ; Transfer patient
 ;
 ;Input
 ;
 ;Required elements include:
 ;  PARAM("DATE")=date time (admission date)
 ;  PARAM("ADMIFN")=admission IFN
 ;  PARAM("TYPE")=type of movement
 ;  PARAM("WARD")=ward
 ;
 ;Optional elements include:
 ;  PARAM("ATNDPHY")=attending physician
 ;  PARAM("ROOMBED")=roombed
 ;  PARAM("DIAG",1)=diagnosis array
 ;  PARAM("PRYMPHY")=primary physician
 ;  PARAM("SERILL")=condition (SERIOUSLY ILL)
 N %,TFN,MVT6
 I TYPE=25!(TYPE=26) S PARAM("WARD")=+LMVT("WARD")
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 I MAS(.05,"I"),$D(PARAM("FTSPEC")),$D(PARAM("ATNDPHY")) D
 . S PARAM("RELIFN")=TFN
 . S %=$$ADDFTS^DGPMAPI6(.MVT6,.PARAM)
 D SETTEVT^DGPMDAL1(TFN,+$G(MVT6),"A")
 D MVTEVT^DGPMAPI1(+PARAM("PATIENT"),2,TFN)
 S RETURN=TFN
 Q 1
 ;
ADDTRA(RETURN,PARAM) ; Add patient transfer
 N %,IFN1,IFN2,IFN6,DT1,DT2,PM2,PM6,MVT2
 S IFN1=+PARAM("ADMIFN"),DT2=+PARAM("DATE"),TYPE=+PARAM("TYPE")
 S PM2(.01)=DT2 ; transfer date
 D ADDMVMTX^DGPMDAL1(.MVT2,.PM2)
 S IFN2=+MVT2 K PM2
 S PM2(.02)=2  ; transaction
 S PM2(.04)=+TYPE  ; type of movement
 S PM2(.03)=+PARAM("PATIENT")  ; patient
 S:$D(PARAM("FCTY")) PM2(.05)=+PARAM("FCTY")  ; patient
 S:$D(PARAM("ASHSEQ")) PM2(.22)=+PARAM("ASHSEQ")  ; ASIH sequence
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2) K PM2
 S PM2(100)=DUZ,PM2(101)=$$NOW^XLFDT()
 S PM2(102)=DUZ,PM2(103)=$$NOW^XLFDT()
 S:$D(PARAM("RABSDT")) PM2(.13)=+PARAM("RABSDT")
 S PM2(.14)=IFN1
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2) K PM2
 S:$D(PARAM("WARD")) PM2(.06)=+PARAM("WARD")  ; ward
 S:$D(PARAM("ROOMBED")) PM2(.07)=+$G(PARAM("ROOMBED"))  ; roombed
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2)
 D UPDPAT^DGPMAPI1(,.PARAM,+PARAM("PATIENT"))
 Q IFN2
 ;
TRANSF(RETURN,PARAM) ; Transfer patient
 N %,TT,MAS,TYPE,ADM
 K RETURN S RETURN=0
 S %=$$CHKTRA(.RETURN,.PARAM,.TYPE,.MAS,.LMVT)
 I %=0 Q 0
 S %=$$LOCKMVT^DGPMDAL1(+PARAM("PATIENT"))
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 D INITEVT^DGPMDAL1
 I 1 D
 . I TYPE=13 S %=$$ASIH(.RETURN,.PARAM) Q
 . I TYPE=43 S %=$$ASIHOF(.RETURN,.PARAM) Q
 . I "^1^2^3^"[(U_+TYPE_U) S %=$$ABS(.RETURN,.PARAM,.LMVT) Q
 . I TYPE=14,LMVT("DISIFN")>0 D DELMVT^DGPMDAL1(LMVT("DISIFN"))
 . S %=$$TRANSF1(.RETURN,.PARAM,TYPE,.MAS,.LMVT) Q
 D ULOCKMVT^DGPMDAL1(+PARAM("PATIENT"))
 Q 1
 ;
ABS(RETURN,PARAM,LMVT) ; Absence transfer
 N TFN,DIS,DMFN
 S PARAM("WARD")=$G(LMVT("WARD"))
 S PARAM("ADMIFN")=$G(LMVT("ADMIFN"))
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D MVTEVT^DGPMAPI1(+DFN,2,TFN)
 S RETURN=+TFN
 Q TFN
 ;
ASIHOF(RETURN,PARAM) ; ASIH (Other facility) transfer
 N %,TFN,DIS,DMFN
 S %=$$GETLASTM^DGPMAPI8(.LMVT,+PARAM("PATIENT"))
 S PARAM("WARD")=+$G(LMVT("WARD"))
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 S %=$$ASHODIS(.RETURN,.PARAM)
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 S RETURN=TFN
 Q TFN
 ;
ASHODIS(RETURN,PARAM) ;
 N %,DIS M DIS=PARAM
 S DIS("TYPE")=34 K DIS("FCTY")
 S DIS("DATE")=$$FMADD^XLFDT(+PARAM("DATE"),30)
 S %=$$ADDDIS^DGPMAPI3(.RETURN,.DIS,+PARAM("PATIENT"),34)
 D SETDEVT^DGPMDAL1(+RETURN,"A")
 Q 1
 ;
ASIH(RETURN,PARAM) ; ASIH transfer
 N %,ADM,TFN,AFN,NADM,TRA,DIS,FTS,FTSFN
 D SETAEVT^DGPMDAL1(+PARAM("ADMIFN"),,"P")
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 M NADM=PARAM
 S NADM("TYPE")=10 ; mvmt type
 S %=$$ADDADM^DGPMAPI1(,.NADM)
 S AFN=+%,TRA(.15)=AFN,TRA(.22)=1
 D UPDMVT^DGPMDAL1(,.TRA,TFN)
 K NADM
 S NADM(.21)=TFN ; asih transfer
 S NADM(.22)=2 ; asih sequence
 D UPDMVT^DGPMDAL1(,.NADM,AFN)
 M DIS=PARAM
 S DIS("TYPE")=34,DIS("DATE")=$$FMADD^XLFDT(+PARAM("DATE"),30)
 S %=$$ADDDIS^DGPMAPI3(.RETURN,.DIS,+PARAM("PATIENT"))
 S DMFN=+RETURN
 M FTS=PARAM
 S FTS("ADMIFN")=AFN,FTS("RELIFN")=AFN
 S %=$$ADDFTS^DGPMAPI6(.RETURN,.FTS)
 S FTSFN=+RETURN
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D SETDEVT^DGPMDAL1(DMFN,"A")
 D SETAEVT^DGPMDAL1(+PARAM("ADMIFN"),,"A")
 D SETAEVT^DGPMDAL1(AFN,FTSFN,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 D UPDPAT^DGPMAPI1(,.PARAM,+PARAM("PATIENT"))
 S RETURN=TFN
 Q 1
 ;
CHKUPD(RETURN,PARAM,TFN,OLD,NEW) ; Check transfer parameters
 N %,TXT,MTYPE,WARD,DATE K RETURN S RETURN=1
 ; transfer date
 S %=$$GETTRA^DGPMAPI8(.OLD,+$G(TFN))
 I OLD=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANFND") Q 0
 I $G(PARAM("DATE"))'="",+$G(OLD("DATE"))'=+$G(PARAM("DATE")) D  Q:'RETURN 0
 . S %=$$GETLASTM^DGPMAPI8(.LMVT,+OLD("PATIENT"),+PARAM("DATE"))
 . I $G(PARAM("DATE"))=""!(+$G(PARAM("DATE"))<1800000) D  Q
 . . S TXT(1)="PARAM('DATE')",RETURN=0 D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 . . ;not before admission
 . I +PARAM("DATE")<+LMVT("ADMDT") S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANBADM") Q
 . I LMVT("TYPE")=3,+PARAM("DATE")>+LMVT("DATE") S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANADIS") Q
 . I $$TIMEUSD^DGPMDAL2(+OLD("PATIENT"),+PARAM("DATE")) D  Q
 . . S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TIMEUSD")
 . S NEW("DATE")=+PARAM("DATE")
 S DATE=$S($D(NEW("DATE")):+NEW("DATE"),1:+OLD("DATE"))
 ; type of movement
 I $G(PARAM("TYPE"))'="",+$G(OLD("TYPE"))'=+$G(PARAM("TYPE")) D  Q:'RETURN 0
 . S %=$$CHKTTYP^DGPMAPI8(.RETURN,+$G(PARAM("TYPE")),+OLD("PATIENT"),DATE) Q:'%
 . S NEW("TYPE")=+PARAM("TYPE")
 S TYPE=$S($D(NEW("TYPE")):+NEW("TYPE"),1:+OLD("TYPE"))
 D GETMVTT^DGPMDAL2(.TT,+TYPE)
 S MTYPE=$G(TT(.03,"I"))
 D GETMASMT^DGPMDAL2(.MAS,MTYPE)
 I MTYPE=23!(MTYPE=25)!(MTYPE=26) S RETURN=1 Q 1
 ; ward 
 I $G(PARAM("WARD"))'="",$G(OLD("WARD"))'=+$G(PARAM("WARD")) D  Q:'RETURN 0
 . S %=$$CHKWARD^DGPMAPI8(.RETURN,$G(PARAM("WARD")),+PARAM("DATE")) Q:'%
 . S NEW("WARD")=+PARAM("WARD")
 S WARD=$S($D(NEW("WARD")):+NEW("WARD"),1:+OLD("WARD"))
 ; roombed
 I $G(PARAM("ROOMBED"))'="",$G(OLD("ROOMBED"))'=$G(PARAM("ROOMBED")) D  Q:'RETURN 0
 . S %=$$CHKBED^DGPMAPI8(.RETURN,+PARAM("ROOMBED"),WARD,+OLD("PATIENT"),DATE) Q:'%
 . S NEW("ROOMBED")=+PARAM("ROOMBED")
 ; related physical movement
 I MAS(.05,"I") D  Q:'% 0
 . S %=$$CHKUPD^DGPMAPI6(.RETURN,.PARAM,+AFN,.NEW) Q:'%
 S RETURN=($D(NEW)>0)
 Q 1
 ;
UPDTRA(RETURN,PARAM,TFN) ; Update transfer
 N %,MVT,OLD,DFN,RPHY K RETURN S RETURN=0
 S %=$$CHKUPD(.RETURN,.PARAM,.TFN,.OLD,.NEW)
 Q:%=0 0
 I RETURN=0 S:'$D(RETURN(0)) RETURN=1 Q $S('$D(RETURN(0)):1,1:0)
 S DFN=$P(OLD("PATIENT"),U)
 S %=$$LOCKMVT^DGPMDAL1(DFN)
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 S RPHY=$$GETRPHY^DGPMDAL1(TFN)
 D INITEVT^DGPMDAL1
 D SETTEVT^DGPMDAL1(TFN,RPHY,"P")
 S:$D(NEW("DATE")) MVT(.01)=+NEW("DATE")
 S:$D(NEW("TYPE")) MVT(.04)=+NEW("TYPE")
 S:$D(NEW("RABSDT")) MVT(.13)=+NEW("RABSDT")
 S:$D(NEW("WARD")) MVT(.06)=+NEW("WARD")
 S:$D(NEW("ROOMBED")) MVT(.07)=+NEW("ROOMBED")
 D UPDMVT^DGPMDAL1(.RETURN,.MVT,TFN)
 S:+RPHY>0 %=$$UPDFTS^DGPMAPI6(.RETURN,.PARAM,RPHY)
 S %=$$UPDPAT^DGPMAPI1(.RETURN,.PARAM,DFN)
 D SETTEVT^DGPMDAL1(TFN,RPHY,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 D ULOCKMVT^DGPMDAL1(DFN)
 S RETURN=1
 Q 1
 ;
CANDEL(RETURN,TFN,TRA,PMVT,NMVT) ; Can delete transfer?
 N FLDS,ADM,ASIH,TXT,MVTT,PTFCO
 S RETURN=0,FLDS=".01;.02;.03;.04;.14;.15;.16;.17;.18;.21"
 D GETMVT^DGPMDAL1(.TRA,TFN,FLDS)
 I TRA=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"TRANFND") Q 0
 D GETMVT^DGPMDAL1(.ADM,TRA(.14,"I"),FLDS)
 D GETPMVT^DGPMDAL3(.PMVT,TRA(.03,"I"),TRA(.14,"I"),,TFN,FLDS)
 D GETNMVT^DGPMDAL3(.NMVT,TRA(.03,"I"),TRA(.14,"I"),,TFN,FLDS)
 I $G(PMVT(.18,"I"))=43,$G(NMVT(.18,"I"))=42 S RETURN=1 Q 1
 I $D(NMVT(.04,"I")),NMVT(.18,"I")'=42,'$$CANFOL^DGPMDAL3(NMVT(.04,"I"),PMVT(.04,"I")) D ERRX^DGPMAPIE(.RETURN,"DELTITP") Q 0
 I "^13^44^"[("^"_TRA(.18,"I")_"^") D ERRX^DGPMAPIE(.RETURN,"DELTMDTA") Q 0
 I TRA(.18,"I")=14,$G(ADM(.17,"I"))>0 D ERRX^DGPMAPIE(.RETURN,"DELTCDWD") Q 0
 D GETMVT^DGPMDAL1(.ASIH,TRA(.15,"I"),FLDS)
 I ASIH D  Q:PTFCO=1 0
 . D GETPTFCO^DGPTDAL1(.PTFCO,ASIH(.16,"I"))
 . I PTFCO=1  D ERRX^DGPMAPIE(.RETURN,"DELTCDPC") Q
 I "^14^43^45^"[("^"_TRA(.18,"I")_"^"),("^13^14^43^44^45^"[("^"_$G(NMVT(.18,"I"))_"^")) D  Q 0
 . D GETMVTT^DGPMDAL2(.MVTT,NMVT(.04,"I"))
 . S TXT(1)=MVTT(.01,"E")
 . D ERRX^DGPMAPIE(.RETURN,"DELTMMRF",.TXT) Q
 S RETURN=1
 Q 1
 ;
DELTRA(RETURN,TFN) ; Delete transfer
 N %,TRA,I,RPHY,NMVT,PMVT,PAR,LMVT,ASH,DIS
 K RETURN S RETURN=0
 D INITEVT^DGPMDAL1
 S %=$$CANDEL^DGPMAPI2(.RETURN,+TFN,.TRA,.PMVT,.NMVT) Q:'RETURN 0
 S %=$$LOCKMVT^DGPMDAL1(PMVT(.03,"I"))
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 S RPHY=$$GETRPHY^DGPMDAL1(+TFN)
 D INITEVT^DGPMDAL1
 D SETTEVT^DGPMDAL1(+TFN,RPHY,"P")
 D:+$G(RPHY)>0 DELMVT^DGPMDAL1(RPHY)
 D DELMVT^DGPMDAL1(+TFN)
 I TRA(.18,"I")=43,NMVT(.18,"I")=42 D DELMVT^DGPMDAL1(NMVT("ID"))
 I TRA(.18,"I")=14,(PMVT(.18,"I")=44)!(PMVT(.18,"I")=13) D
 . D GETMVT^DGPMDAL1(.ASH,PMVT(.15,"I"))
 . D DELMVT^DGPMDAL1(ASH(.17,"I"))
 . S DIS("TYPE")=34,DIS("ADMIFN")=PMVT(.14,"I")
 . S DIS("DATE")=$$FMADD^XLFDT(PMVT(.01,"I"),30)
 . S %=$$ADDDIS^DGPMAPI3(.RETURN,.DIS,ASH(.03,"I"))
 I TRA(.18,"I")=14,PMVT(.18,"I")=43 D
 . S %=$$GETLASTM^DGPMAPI8(.LMVT,PMVT(.03,"I"),PMVT(.01,"I"))
 . S PAR("PATIENT")=PMVT(.03,"I")
 . S PAR("DATE")=PMVT(.01,"I"),PAR("ADMIFN")=LMVT("ADMIFN")
 . S %=$$ASHODIS(.RETURN,.PAR)
 D SETTEVT^DGPMDAL1(+TFN,RPHY,"A")
 D MVTEVT^DGPMAPI1(TRA(.03,"I"),2,+TFN)
 D ULOCKMVT^DGPMDAL1(PMVT(.03,"I"))
 S RETURN=1
 Q 1
 ;
DELASH(RETURN,TFN) ; Delete ASIH transfer
 N TRA,ADM,FLDS
 K RETURN S RETURN=0
 S FLDS=".01;.02;.03;.04;.14;.15;.16;.17;.18;.21"
 D GETMVT^DGPMDAL1(.TRA,TFN,FLDS)
 D GETMVT^DGPMDAL1(.ADM,TRA(.14,"I"),FLDS)
 D DELMVT^DGPMDAL1(+ADM(.17,"I"))
 D DELMVT^DGPMDAL1(+TFN)
 D SETDEVT^DGPMDAL1(+ADM(.17,"I"),"A")
 D SETTEVT^DGPMDAL1(+TFN,,"A")
 S RETURN=1
 Q 1
 ;
