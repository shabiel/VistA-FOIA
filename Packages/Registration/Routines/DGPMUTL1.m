DGPMUTL1 ;ALB/MJK - PATIENT MOVEMENT UTILS ; 12/14/2012 ;
 ;;5.3;Registration;;
SELECT(PARAM,LIST,DEFVAL) ; Select an item from list
 N IFN
 D SELECT1
 Q $G(IFN)
 ;
SELECT1 ;
 N DIR,TXT,RTN,FLDS,NAME,LST,SFLDS K DIR
 S RTN=$G(PARAM("RTN")),FLDS=$G(PARAM("FLDS"))
 S SFLDS=$G(PARAM("SRCHFLDS"))
 I FLDS="" S FLDS="NAME"
 S NAME=$G(PARAM("ENTITY"))
 M LST=LIST
 S DIR("A")=$G(PARAM("PROMPT"))
 S DIR("?")="^D HELP1^DGPMUTL1"
 S DIR("??")="^D HELP2^DGPMUTL1"
 S:'$D(DIR(0)) DIR(0)="FOr"
 I $D(DEFVAL),DEFVAL'="" S DIR("B")=$P(DEFVAL,U,2)
 D ^DIR
 I $G(PARAM("FLAG"))["R",Y=""!(Y="^") D HLP(1) G SELECT1
 Q:Y=""!(Y="^")
 S TXT=Y
 I Y=$P($G(DEFVAL),U,2) S IFN=$P(DEFVAL,U) Q
 I $D(LIST) S IFN=$$SEARCH(TXT,.LIST)
 E  S IFN=$$LIST(RTN,.FLDS,.LIST)
 G:IFN<0 SELECT1
 Q IFN
 ;
SEARCH(TXT,LIST) ; Search text in list
 N I,F,FLD,IFN,FDS
 S IFN=-1,FDS=$S($G(SFLDS)'="":SFLDS,1:FLDS)
 F I=0:0 S I=$O(LIST(I)) Q:I=""!(IFN>0)  D
 . F F=1:1 S FLD=$P(FDS,";",F) Q:FLD=""  D
 . . I $$UP^XLFSTR(LIST(I,FLD))[$$UP^XLFSTR(TXT) S IFN=LIST(I,"ID")
 Q IFN
 ;
LIST(RTN,FLDS,LIST) ; Choose from list
 N DIR,CNT,LST,TMPLS,IND
 S CNT=0
 D PRINT(RTN,FLDS,.LIST)
 I +$G(IND)>0,$D(LST(+IND)) S IFN=LST(+IND,"ID")
 E  S IFN=-1
 Q IFN
 ;
PRINT(RTN,FLDS,LIST) ; Print partial list
 N MSG,R1,LINE,QUIT,SEL,START,NO
 S QUIT=0 K LST
 I $D(LIST) M LST=LIST
 E  S R1=RTN_"(.LST,.TXT,.START,.NO)" D @R1
 S LINE=0
 F I=0:0 S I=$O(LST(I)) Q:I=""!(QUIT)  D
 . N LN S LN="",QUIT=0
 . S LINE=LINE+1
 . S:LST(0)>1 LN=$C(32,32)_LINE_$C(32,32)
 . F F=1:1 S FLD=$P(FLDS,";",F) Q:FLD=""  D
 . . Q:FLD="ID"
 . . S LN=LN_$C(9)_LST(I,FLD)
 . . S SEL(LINE)=$G(SEL(LINE))_$C(9)_LST(I,FLD)
 . . S MSG(LINE)=LN
 . S CNT=LINE
 . I LINE#5=0  D
 . . D EN^DDIOL(.MSG)
 . . S IND=$$CHOOSE()
 . . I IND'="" S QUIT=1 Q:$D(QUIT)  Q
 I LINE#5,'QUIT D EN^DDIOL(.MSG) S IND=$$CHOOSE()
 I CNT>1,IND>0 D EN^DDIOL(SEL(IND))
 I LINE=0 D HLP(1)
 Q
CHOOSE() ;
 N TXT
 Q:CNT=0 0
 S TXT(1)=CNT
 S DIR("A")=$$EZBLD^DIALOG(1250000.084,.TXT)
 S DIR(0)="FOA"
 D:CNT>1 ^DIR
 S:+Y>CNT Y=0
 S:CNT=1 Y=1
 Q Y
HELP1 ; Display help and list
 N HLST,TXT,SCH,START,NO,%
 D HLP(1)
 I $D(LIST) M LST=LIST D PRINTALL(RTN,FLDS) Q
 E  S R1=RTN_"(.LST,.SCH,.START,.NO)" D @R1
 D HLP(3)
 S TXT(1)=+LST(0),TXT(2)=$$UP^XLFSTR(NAME)
 S HLST=$$EZBLD^DIALOG(1250000.088,.TXT)
 D EN^DDIOL(HLST)
 S %=0 D YN^DICN I %=1 D PRINTALL(RTN,FLDS)
 Q
 ;
HLP(NO) ; Display help NO
 N HLP,I,IF S IF=0
 D BLD^DIALOG($G(PARAM("HELP"_NO)),,,"HLP")
 D EN^DDIOL(.HLP)
 Q
HELP2 ; Display help and list
 D HLP(2)
 D PRINTALL(RTN,FLDS)
 Q
 ;
PRINTALL(RTN,FLDS) ; Print entire list
 N MSG,R1,QUIT,START,NO,SCH
 S QUIT=0,LINE=0
 I $D(LIST) M LST=LIST
 E  S R1=RTN_"(.LST,.SCH,.START,.NO)" D @R1
 D EN^DDIOL($$EZBLD^DIALOG(8068))
 F I=0:0 S I=$O(LST(I)) Q:I=""!(QUIT)  D
 . N LN S LN=""
 . S LINE=LINE+1
 . F F=1:1 S FLD=$P(FLDS,";",F) Q:FLD=""  D
 . . S LN=LN_$C(9)_LST(I,FLD)
 . . S MSG(LINE)=LN
 . I LINE>(IOSL-4) D EN^DDIOL(.MSG) S LINE=1 K MSG S:'$$CONTINUE QUIT=1 Q:$D(QUIT)  Q
 D:LINE<(IOSL-4) EN^DDIOL(.MSG)
 Q
CONTINUE() ; -- end of page prompt
 N DIR,X,Y
 S DIR(0)="E",DIR("A")=$C(9)_$$EZBLD^DIALOG(1250000.086)
 D ^DIR
 Q +Y
 ;
SELTEXT(PARAM,DEFVAL) ; Get text
 N DIR,X,Y,EXTXT
 S DIR(0)="FOr^"_$G(PARAM("MIN"))_":"_$G(PARAM("MAX"))
 S DIR("A")=PARAM("PROMPT")
 S:$D(DEFVAL) DIR("B")=DEFVAL
 S DIR("?")="^D HLP^DGPMUTL1(1)"
 S DIR("??")="^D HLP^DGPMUTL1(2)"
 S EXTXT=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.017))
SELTEXT1 ;
 D ^DIR
 I $G(PARAM("FLAG"))["R",Y=""!(Y="^") D:Y="" HLP(1) D:Y="^" EN^DDIOL(EXTXT) G SELTEXT1
 I $G(PARAM("VALID"))'="" D @(PARAM("VALID")_"(X,.V)") S:V=-1 Y=X S:V>0 Y=V I 'V D HLP(1) G SELTEXT1
 Q Y
 ;
YN(PARAM,DEFAULT) ;
 N %,PROMPT,HELP
 S PROMPT=$S($D(PARAM("PROMPTT")):PARAM("PROMPTT"),1:$$EZBLD^DIALOG($G(PARAM("PROMPT"))))
 S HELP=$S($D(PARAM("HELPT")):PARAM("HELPT"),1:$$EZBLD^DIALOG($G(PARAM("HELP"))))
 G ASK1
 Q %
ASK1 ;
 D:$D(%) EN^DDIOL(HELP)
 D EN^DDIOL(PROMPT)
 S %=$G(DEFAULT) D YN^DICN I '% G ASK1
 Q %
 ;
